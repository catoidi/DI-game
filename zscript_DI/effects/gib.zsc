//zscript_DI/effects/gib.zsc

Class DI_Gib : DI_FloorActor
{
	Default
	{
		Radius 8;
		Height 8;
		Mass 0;
		BounceType "Doom";
		BounceFactor 0.4;
		WallBounceFactor 0.8;
	
		-SOLID;
		+NOBLOCKMAP;
		+NOTELEPORT;
		+ALLOWPARTICLES;
		+THRUACTORS;
		+MISSILE;
		+ROLLSPRITE;
		+SPRITEANGLE;
		+EXPLODEONWATER;
	}
	
	override void Tick(void)
	{
		super.tick();
		
		if (!InStateSequence(CurState, ResolveState("Death")))
		{
			AliveTick();
		}
	}
	
	virtual void AliveTick(void)
	{
		if (self.Pos.Z ~== self.FloorZ) {A_Die();}
	}
	
	override void postbeginplay(void)
	{
		super.postbeginplay();
		self.Vel += (frandom(-2, 2), frandom(-2, 2), frandom(-2, 2));
	}
	
	action void GibStart()
	{
		self.Scale.X *= frandom(0.5, 1.2); 
		self.Scale.Y *= frandom(0.5, 1.2);
		A_SetRoll(random(0, 359));
	}

	States
	{
		Spawn:
			TNT1 AAA 0 NoDelay A_Jump(256, "Roller");
			Stop;
		Roller:
			#### # 0 GibStart();
			BLUD D 15;
			#### # 0 A_Jump(256, "Looper");
			Stop;
		Looper:
			#### # 15;
			#### # 0 {A_FadeOut(0.1);}
			Loop;
		Death.Sky:
		Death:
			#### # 0 {bFLATSPRITE = true; invoker.Alpha = invoker.default.Alpha;}
			#### # -1 ReadyToClean();
			Wait;
		Looper:
			#### # 1 A_FadeOut(0.05);
			Loop;
	}
}

Class DI_GibBloody : DI_Gib
{
	int randomblood;
	int bloodtimer;
	string trailactor;
	
	Property TrailActor: trailactor;
	
	Default 
	{
		DI_GibBloody.TrailActor "DI_BloodPuff_Gib";
		+USEBOUNCESTATE;
	}
	
	override void postbeginplay()
	{
		super.postbeginplay();
		
		randomblood = random(4, 8);
	}

	override void AliveTick()
	{
		super.AliveTick();
		
		++bloodtimer;
		if (bloodtimer >= randomblood)
		{
			DoGibTrail();
			//A_SpawnItemEX("DI_BloodPuff_Gib", flags: SXF_NOCHECKPOSITION | SXF_TRANSFERTRANSLATION | SXF_TRANSFERALPHA);
			bloodtimer = 0;
		}
	}
	
	virtual void DoGibTrail(float trailsize = 1.0)
	{
		//has to be an actor as we cant transfer translations to particles.
		bool spawned;
		actor item;
		[spawned, item] = A_SpawnItemEX(trailactor, flags: SXF_NOCHECKPOSITION | SXF_TRANSFERTRANSLATION | SXF_TRANSFERALPHA);
		if (item) 
		{
			item.Scale.X *= trailsize;
			item.Scale.Y *= trailsize;
		}
	}
	
	virtual void GibEnd()
	{
		Alpha = default.Alpha;
		bFLATSPRITE = true;
		DoGibTrail(2.0);
	}
	
	States
	{
		Bounce:
			#### # 0 DoGibTrail(2.0); //A_SpawnItemEX("DI_BloodPuff_GibBig", flags: SXF_NOCHECKPOSITION | SXF_TRANSFERTRANSLATION | SXF_TRANSFERALPHA);
			#### # 0 A_Jump(256, "Looper");
		Death:
			#### # 0 GibEnd();
			#### # -1 ReadyToClean();
	}
}

Class DI_RealBoxGib : DI_Gib
{
	//the sprite and frame are assigned by the spawner of the gib.
	//see the "BoxGib" function in the Box Party Enemy mixin for more details.
	
	string boxsprite;
	int boxframe;
	
	States
	{
		Roller:
			#### # 0 GibStart();
			TNT1 A 15
			{
				sprite = GetSpriteIndex(invoker.boxsprite);
				frame = invoker.boxframe;
			}
			#### # 0 A_Jump(256, "Looper");
			Stop;
	}
}

Class DI_GibExplosion : DI_Actor
{
	Default
	{
		Radius 8;
		Height 8;
		Mass 0;
		RenderStyle "Translucent";
		Alpha 0.8;
		Scale 3.0;
		BounceType "Doom";
	
		-SOLID;
		+NOINTERACTION;
		+NOBLOCKMAP;
		+NOTELEPORT;
		+ROLLSPRITE;
		+SPRITEANGLE;
	}
	
	int gibamount;
	float boomsize;
	int randomblood;
	
	override void PostBeginPlay()
	{
		super.postbeginplay();
		
		randomblood = random(0, 2);
		
		if (target)
		{
			gibamount = (target.height * target.radius) / 50;
			if (gibamount < 2) {gibamount = 2;}
			if (gibamount > 24) {gibamount = 24;}
			boomsize = (target.height + target.radius) / 48;
			Scale *= boomsize;
		}
		else {gibamount = 4;}
	}
	
	override void Tick()
	{
		super.tick();
		
		if (level.IsFrozen() == false) {Vel.Z += 0.05;}
	}
	
	virtual name GetGib(int gibnum = 0)
	{
		return "DI_GibBloody";
	}
	
	action void StartGibbing(bool translated = true)
	{
		int spawnflag = SXF_NOCHECKPOSITION;
		if (translated) {spawnflag += SXF_TRANSFERTRANSLATION | SXF_TRANSFERRENDERSTYLE;}
		A_SetRoll(random(0, 359));
		for (int i = 0; i < invoker.gibamount; ++i)
		{
			let gibvel = invoker.boomsize * 9;
			A_SpawnItemEX(invoker.GetGib(i), 0, 0, 0, frandom(-gibvel, gibvel), frandom(-gibvel, gibvel), frandom(gibvel / 2, gibvel * 2), random(0, 359), spawnflag, 0, invoker.tid);
		}
		A_StartSound("wilhelm");
	}
	
	action void GibAway(int gibframe = -1)
	{
		if (gibframe > -1) {frame = gibframe;}
		A_DIScale(1.025, 1.025, true); 
		A_FadeOut(0.05);
	}

	States
	{
		Spawn:
			TNT1 A 0 NoDelay StartGibbing();
			BLUD A 1 GibAway(invoker.randomblood);
			Wait;
	}
}