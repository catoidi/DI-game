CLASS DI_Blood : DI_Actor replaces Blood
{
	Default
	{
		Radius 8;
		Height 8;
		Mass 0;
		RenderStyle "Translucent";
		Alpha 0.8;
		Scale 0.6;
		BounceType "Doom";
	
		-SOLID;
		+NOBLOCKMAP;
		+NOTELEPORT;
		+ALLOWPARTICLES;
		+THRUACTORS;
		+MISSILE;
		+ROLLSPRITE;
		+SPRITEANGLE;
		-BOUNCEONFLOORS;
		+EXPLODEONWATER;
	}
	
	override void tick(void)
	{
		super.tick();
		
		if (!InStateSequence(CurState, ResolveState("Death")))
		{
			if (self.Pos.Z ~== self.FloorZ) {A_Die();}
		}
	}
	
	override void postbeginplay(void)
	{
		super.postbeginplay();
		self.Vel += (frandom(-2, 2), frandom(-2, 2), frandom(-2, 2));
	}
	
	States
	{
		Spawn:
			TNT1 AAA 0 NoDelay A_Jump(256, "Roller");
			Stop;
		Roller:
			#### # 0 
			{
				frame = random(0, 2);
				self.Scale.X *= frandom(0.5, 1.2); 
				self.Scale.Y *= frandom(0.5, 1.2);
				A_SetRoll(random(0, 359));
			}
			BLUD # 15 A_SpawnItemEX("DI_BloodPuff", flags: SXF_NOCHECKPOSITION | SXF_TRANSFERTRANSLATION | SXF_TRANSFERSPRITEFRAME | SXF_TRANSFERSCALE | SXF_TRANSFERALPHA | SXF_TRANSFERRENDERSTYLE | SXF_TRANSFERROLL);
		Looper:
			#### # 15 {A_FadeOut(0.2); A_DIScale(1.2, 1.2, true);}
			Loop;
		Death.Sky:
		Death:
			#### # 0;
			//#### # 0 A_Jump(96, "DeathLoop");
			#### # 0 
			{if (self.Pos.Z ~== self.FloorZ) {A_SpawnItemEX("DI_BloodFlat", 0, 0, 0, 0, 0, 0, random(0, 359), SXF_NOCHECKPOSITION | SXF_TRANSFERTRANSLATION | SXF_TRANSFERSPRITEFRAME | SXF_TRANSFERSCALE | SXF_TRANSFERALPHA | SXF_TRANSFERRENDERSTYLE);}}
			Goto DeathLoop;
		DeathLoop:
			#### # 1 A_DIScale(1.2, 1.2, true);
			#### # 0 A_FadeOut(0.2);
			Loop;
	}
}

CLASS DI_BloodFlat : DI_FloorActor 
{
	Default
	{
		Radius 8;
		Height 8;
		Mass 0;
		Scale 0.2;
		RenderStyle "Translucent";
		Alpha 0.8;
	
		-SOLID;
		-BLOCKEDBYSOLIDACTORS;
		+INTERPOLATEANGLES;
		+FLATSPRITE;
		+SPRITEANGLE;
	}
	
	States
	{
		Spawn:
			TNT1 A 0 A_SetRoll(random(0, 359)); //inherits sprite/frame from parent.
			#### #################### 1 A_DIScale(1.03, 1.03, true);
			#### # 1050 A_SetTics(floortic);
			Goto Looper;
		Looper:
			#### # 1 A_FadeOut(0.05);
			Loop;
	}
}
Class DI_BloodSplatter : DI_Blood replaces BloodSplatter {}
Class DI_AxeBlood : DI_Blood replaces AxeBlood {}

CLASS DI_BloodPuff : DI_Actor
{
	Default
	{
		Scale 1.5;
		+NOINTERACTION;
		+ROLLSPRITE;
		+ROLLCENTER;
		+NOTELEPORT;
		+SPRITEANGLE;
		-SOLID;
	}
	
	States
	{
		Spawn:
			TNT1 A 1 NoDelay A_DIScale(1.3, 1.3, true);
			#### # 1 {A_DIScale(1.05, 1.05, true); A_FadeOut(0.05);}
			Wait;
	}
}