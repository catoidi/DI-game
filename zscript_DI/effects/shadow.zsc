//What this does:
//This creates a square shadow at every player's feet that follows the player's X and Y coords.
//Then it gets where the Z axis is supposed to be using FloorZ.

//Known Issues:
//Whenever standing on a 3d floor, for some reason the alpha of the shadow is doubled????? Still don't 100% understand this one.

CLASS DIShadow : Actor
{
	Default
	{
		FloatBobPhase 0;
		RenderStyle "Translucent";
		+MOVEWITHSECTOR;
		+FLATSPRITE;
		+NOTONAUTOMAP;
		+NOBLOCKMAP;
		+NOINTERACTION;
		+INTERPOLATEANGLES;
	}

	override void Tick(void)
	{
		Super.Tick();
		
		if (master)
		{
			//Thank Nash's old SpriteShadow mod for this crazy math stuff here.
			Vector3 sPos = (
				master.Pos.X + cos(players[consoleplayer].camera.Angle) * 0.01, 
				master.Pos.Y + sin(players[consoleplayer].camera.Angle) * 0.01, 
				master.FloorZ
			); 
			SetOrigin(sPos, true);
			Angle = master.Angle;
			Alpha = master.Alpha * 0.5;
			
			double Scaler = 1.0 * (master.Radius / 16.0);
			scale.X = Scaler;
			scale.Y = Scaler;
		}
		else {Destroy();}
	}
	
	States
	{
		Spawn:
			SHAD A -1;
			Loop;
	}
}