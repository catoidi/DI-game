//zboxscript/house/painting.zsc

CLASS Painting : DI_Actor
{
	int user_renderdistance;
	//$UserDefaultValue 1500
	//this isn't for performance reasons, this is a workaround for flatsprite positions getting more and more incorrect the farther they are away from the camera.
	//if placed on a thin wall, i recommend lower values. otherwise, just increase the value if you notice any pop-ins.
	
	Property RenderDistance: user_renderdistance;

	Default
	{
		Height 64;
		Radius 16;
		Activation THINGSPEC_Switch;
		Painting.RenderDistance 1500;
		-SOLID;
		//+NOINTERACTION; //disabling this will potentially cause performance loss, but is necessary to be able to interact with the paintings.
		+NOLIFTDROP;
		+NOGRAVITY;
		+THRUACTORS;
		+WALLSPRITE;
		+SPRITEANGLE;
		+USESPECIAL; //dont place this object above/below doors or switches
		//$Category "DI/Decor/Building"
		//$Title "Painting"
		//$Color 4
	}
	
	static const float spriterotation[] = {180.0, 180.0, 225.0, 270.0, 315.0, 0.0, 45.0, 90.0, 135.0, 202.5, 247.5, 292.5, 337.5, 22.5, 67.5, 112.5, 157.5};
	string flavortext;
	
	override void PostBeginPlay()
	{
		super.postbeginplay();
		
		if (flavortext == "") {flavortext = "PAINTING_NOTEXT";}
	}
	
	override void Activate(Actor activator)
	{
		if (CheckRange(64) == false) {PaintingUsed();}
	}
	
	override void Deactivate(Actor activator)
	{
		if (CheckRange(64) == false) {PaintingUsed();}
	}
	
	virtual void PaintingUsed()
	{
		console.printf("%s", stringtable.Localize(flavortext, false));
	}
	
	action void DoPaintingRotation(int rot) {self.SpriteAngle = invoker.spriterotation[rot];}
	
	States
	{
		Spawn:
			PANT Z 0 NoDelay
			{
				if (GetCVAR("box_fun") == 12)
				{
					SetStateLabel("Pride");
				}
			}
			Goto Looper;
		Pride:
			PANT Z 1;
			PANT D 0 DoPaintingRotation(random(1, 2));
			Goto Looper;
		Looper:
			#### # 5
			{
				if (CheckRange(invoker.user_renderdistance)) {A_FadeTo(0.0, 0.35, FTF_CLAMP);}
				else {A_FadeTo(1.0, 0.35, FTF_CLAMP);}
			}
			Loop;
		//this state is loaded into memory, but not used by any functions.
		DummyState: //gotta define these frames within any actor's state or the game will crash if ZScript tries to load it.
			PANA A 1;
			PANB A 1; //big paintings.
			PANC A 1;
			PAND A 1;
			PANE A 1;
			PANF A 1;
			PANG A 1;
			PANH A 1;
			PANI A 1;
			PANJ A 1;
			PANK A 1;
			PANL A 1;
			PANM A 1;
			PANN A 1;
			PANO A 1;
			PANP A 1;
			PANQ A 1;
			PANR A 1;
			PANS A 1;
			PANT A 1; //most paintings go here.
			PANU A 1;
			PANV A 1;
			PANW A 1;
			PANX A 1;
			PANY A 1;
			PANZ A 1;
			Stop;
	}
}

Class StaticPainting : Painting
{
	//behaves how it does in box party now.
	//this should be used for any painting the player cannot normally reach.
	Default 
	{
		+NOINTERACTION;
		-USESPECIAL;
		//$Title "Painting (Static)"
	}
}