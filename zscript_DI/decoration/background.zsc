//zscript_DI/decoration/background.zsc

//decoration objects for use with skyboxes.

Class Buildings : DI_Decor
{
	Default
	{
		Scale 2;
		Radius 50;
		Height 256;
		+SOLID;
		+BRIGHT;
		+WALLSPRITE;
		//$Category "DI/Decor/Background/"
		//$Title "Building"
		//$Sprite "BUILA0"
		//$Color 2
	}
	
	States
	{
		Spawn:
			TNT1 A 0 NoDelay A_Jump(128, "Null");
			BUIL A 0
			{
				int randomnum = random(1, 100);
				if (randomnum < 90) {frame = random(0, 3);}
				else
				{
					if (randomnum < 100) {frame = 4;}
					else {frame = 5;}
				}
			}
			"####" "#" 0 A_DIDecor();
			"####" "#" -1;
			Stop;
	}
}

Class Mountains : Buildings
{
	Default 
	{
		-BRIGHT;
		//$Title "Mountain"
		//$Sprite "MOUNA0"
		//$Color 2
	}

	States
	{
		Spawn:
			TNT1 A 0 NoDelay A_Jump(220, "Null");
			TNT1 A 0 A_Jump(256, "Firsty", "Firsty", "Firsty", "Firsty", "Firsty", "Firsty", "Secondy", "Secondy", "Secondy", "Secondy", "Secondy", "Thirdy");
			PBOX A -1;
			Stop;
		Firsty:
			MOUN A 0;
			Goto Flipper;
		Secondy:
			MOUN B 0;
			Goto Flipper;
		Thirdy:
			MOUN C 0;
			Goto Flipper;
		Flipper:
			"####" "#" 0 A_DIDecor();
			"####" "#" -1;
			Stop;
	}
}

Class Coral : Buildings
{
	Default
	{
		-BRIGHT;
		//$Title "Coral (Skybox)"
		//$Sprite "CORLA0"
		//$Color 2
	}
	
	States
	{
		Spawn:
			TNT1 A 0 NoDelay A_Jump(192, "Null");
			TNT1 A 0 A_DIDecor();
			CORL A 0 {frame = random(0, 5);}
			#### # -1;
			Stop;
	}
}