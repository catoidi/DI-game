//zscript_DI/enemies/basic/ron.zsc

Class Ron : DI_Monster
{
	Default
	{
		Speed 5;
		Health 50;
		Radius 16;
		Height 60;
		Mass 160;
		//Species "Ron";
		BloodColor "Blue";
		DamageFactor "Poison", 1.2;
		DamageFactor "FakePoison", 1.2;
		DamageFactor "Fire", 0.9;
		DamageFactor "FakeFire", 0.9;
		-THRUSPECIES;
	}
	
	action void A_DIRonBullet(void)
	{
		A_StartSound("bullet");
		A_FaceTarget();
		A_DIScale(0.1, -0.1);
		A_SpawnProjectile("Ron_Bullet", 37, 0, 0, CMF_OFFSETPITCH);
	}
	
	States
	{
		Spawn:
			RONN A 10 A_Look;
			Loop;
		See:
			#### AABB 2 A_Wander();
			Goto RealSee;
		RealSee:
			#### AABB 2 A_Chase();
			Loop;
		Missile:
			#### D 3 BRIGHT A_DIRonBullet();
			#### C 17;
			#### # 0 A_DIScale(-0.1, 0.1);
			Goto See;
	}
}

CLASS Ron_Bullet : DI_Bullet
{
	
}

Class Ron2 : Ron
{
	Default
	{
		Mass 80;
		Speed 10;
		Health 150;
		Scale 1.25;
		Radius 20;
		Height 75;
		Tag "Fiendish Ron";
		DamageFactor "Ice", 1.4;
		DamageFactor "FakeIce", 1.4;
		DI_Monster.DIDrop "DI_Money";
	}
	
	action void A_DIRon2Bullet()
	{
		A_StartSound("bullet");
		A_FaceTarget();
		A_DIScale(0.15, -0.15);
		A_SpawnProjectile("Ron2_Bullet", 46, 0, 0, CMF_OFFSETPITCH);
	}
	
	States
	{
		Spawn:
			RONN F 10 A_Look;
			Loop;
		See:
			#### FFGGFFGGFFGG 2 A_Wander();
			Goto RealSee;
		RealSee:
			#### FFGG 2 A_Chase();
			Loop;
		Missile:
			#### HHH 5 A_DIScale(-0.05, 0.05);
			#### I 3 BRIGHT A_DIRon2Bullet();
			#### H 4 A_DIScale(-0.075, 0.075);
			#### I 3 BRIGHT A_DIRon2Bullet();
			#### H 4 A_DIScale(-0.075, 0.075);
			#### I 3 BRIGHT A_DIRon2Bullet();
			#### H 4 A_DIScale(-0.075, 0.075);
			#### H 17;
			#### # 0 A_ResetScale();
			Goto See;
	}
}

Class Ron2_Bullet : Ron_Bullet
{
	Default
	{
		DamageFunction (12);
		Radius 15;
		Height 30;
		Scale 1.5;
		DI_Bullet.Trail "RONNJ0";
		DI_Bullet.FancyLight "GLOWPURPLE";
	}
	
	States
	{
		Spawn:
			RONN J 0 BRIGHT NoDelay A_Jump(256, "Spinner");
			Stop;
	}
}