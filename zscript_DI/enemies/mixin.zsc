mixin class DI_MonsterRobotMixin
{
	Default
	{
		//multiply the health of the robot somewhere between 2x and 7x, depending on how tanky the original actor already was.
		//multiply mass of parent actor by 100.
		PainChance 0;
		PainChance "Lightning", 256;
		PainChance "FakeLightning", 256;
		DamageFactor "Lightning", 1.6;
		DamageFactor "FakeLightning", 1.6;
		DamageFactor "Fire", 0.9;
		DamageFactor "FakeFire", 0.9;
		DamageFactor "Poison", 0.8;
		DamageFactor "FakePoison", 0.8;
		ExplosionDamage 25;
		ExplosionRadius 64;
		+NOBLOOD;
		-THRUSPECIES; //machines cannot phase through other enemies.
	}
	
	States
	{
		Death:
			#### # 0 A_DITargetPlayer();
			#### # 0 A_DIDie();
			#### # 0 A_Jump(256, "DeathLoop");
			Stop;
		Death.Lightning:
			#### # 0 
			{
				A_DITargetPlayer();
				A_AttachLightDef("statuslight", "GLOWREDBIG");
				A_SetTranslation("DamRed");
				bBRIGHT = true;
			}
			#### ########## 3 A_DISmokin();
			#### # 0
			{
				A_DISpawnBloodCoins();
				A_Explode(flags: XF_NOTMISSILE | XF_EXPLICITDAMAGETYPE, damagetype: "Enemy");
				A_SpawnItemEX("SafeBoom", frandom(-4, 4), frandom(-4, 4), frandom(0, 8), frandom(-4, 4), frandom(-4, 4), frandom(0, 4), 0, SXF_NOCHECKPOSITION);
				A_SpawnItemEX("BoomText", frandom(-4, 4), frandom(-4, 4), frandom(0, 8), frandom(-4, 4), frandom(-4, 4), frandom(0, 4), 0, SXF_NOCHECKPOSITION);
			}
			#### # 0 A_DIDie();
			#### # 0 A_Jump(256, "DeathLoop");
			Stop;
		XDeath:
			#### # 0 A_Jump(256, "Death.Lightning");
			Stop;
		XDeath.Ice:
			#### # 0 A_DISpawnBloodCoins();
			#### # 0 A_DIIceDeath(); //di stuff
			#### # 0 A_FreezeDeath(); //gzdoom stuff
			#### # 35 A_FreezeDeathChunks();
			Wait;
	}
}

mixin class DI_BoxBloodMixin
{
	static const string alphabet[] = {"A", "B", "C", "D", "E", "F", "G", "H", "I", "J", "K", "L", "M", "N", "O", "P", "Q", "R", "S", "T", "U", "V", "W", "X", "Y", "Z", "[", "]"}; //all characters you can use sprite frames for, excluding \.

	action void DI_BoxGib(string boxspr, double boxsize = 4.0)
	{
		if (random(0, 4) > 0)
		{
			A_SpawnParticleEX("White", TexMan.CheckForTexture(boxspr), 0, SPF_ROLL, 24, boxsize, random(0, 359), random(-2, 2), random(-2, 2), random(-2, 2), random(-2, 2), random(-2, 2), random(-2, 3), 0, 0, -0.33, 1, 0, -0.16, random(0, 359), 3 - (6 * (random(0, 1))));
		}
		else
		{
			bool spawned;
			actor gib;
			[spawned, gib] = A_SpawnItemEX("DI_RealBoxGib", random(-2, 2), random(-2, 2), random(-2, 2), random(-2, 2), random(-2, 2), random(-2, 3), random(0, 359), SXF_NOCHECKPOSITION);
			
			if (gib)
			{
				int doframe = -1;
				let realgib = DI_RealBoxGib(gib);
				realgib.boxsprite = boxspr;
				let realframe = boxspr.Mid(4, 1);
				for (int i = 0; i < invoker.alphabet.size(); ++i)
				{
					if (realframe ~== invoker.alphabet[i])
					{
						realgib.boxframe = i;
						i = invoker.alphabet.size();
					}
				}
			}
		}
	}
}

mixin class DI_FiendishMixin
{
	bool dofienddodge;

	//allows fiendish enemies to dodge.
	override void tick()
	{
		super.tick();
		if (level.isfrozen() == false)
		{
			if (dofienddodge)
			{
				angle = random(0, 359);
				A_Recoil(10);
				ThrustThingZ(0, 10, 0, 1);
				dofienddodge = false;
			}
		}
	}
	
	override int DamageMobj(Actor inflictor, Actor source, int damage, Name mod, int flags, double angle) 
	{
		if ((source is "PlayerPawn") && (damage > 0) && (mod != 'Lightning') && (mod != 'Melee') && (mod != 'FakeLightning')) {dofienddodge = true;}
		return super.DamageMobj(inflictor, source, damage, mod, flags, angle);
	}
}