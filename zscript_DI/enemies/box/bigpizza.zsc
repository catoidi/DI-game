//zscript_DI/enemies/box/bigpizza.zsc

Class Pizzaulter : DI_Monster
{
	Default
	{
		Speed 6;
		Health 800;
		Radius 24;
		Height 40;
		Mass 4000;
		DamageFactor "Melee", 1.5;
		DamageFactor "Poison", 1.25;
		DamageFactor "FakePoison", 1.25;
		DI_Monster.GibType "DI_PizzaExplosion";
		DI_Monster.DIDrop "DI_Coin";
		DI_Monster.CanSwim false;
		DI_Actor.DontScale true; //rotating sprites shouldn't be scaled
		BloodType "PizzaBlood";
		Tag "Pizzaulter";
		Obituary "$BOX_DIE_PIZZA_BIG";
		+FLOAT;
		+NOGRAVITY;
		+ROLLSPRITE;
		+ROLLCENTER;
		+INTERPOLATEANGLES;
		+NOBLOODDECALS;
		+FORCEXYBILLBOARD;
		-CASTSPRITESHADOW;
		//$Category "Box Party/Enemies/"
		//$Title "Pizza (Big)"
		//$Color 4
	}
	
	override void PostBeginPlay()
	{
		Super.PostBeginPlay();
		A_SpawnItemEX("DIShadow", 0, 0, 0, 0, 0, 0, 0, SXF_SETMASTER);
	}
	
	action void A_PizzaAttack (void)
	{
		int rnum = roll;
		while (rnum < 0) {rnum += 360;}
		while (rnum > 360) {rnum -= 360;}
		A_SpawnProjectile("PepperoniBullet", (30 + (rnum/90)), (-4 + (rnum/45)), (-9 + (rnum/20)), 0, (-6 + (rnum/30)));
		A_SetRoll(roll - 40);
		A_StartSound("peppow", volume: 0.25, pitch: 1.5);
	}
	
	States
	{
		Spawn:
			PIZZ A 0 NoDelay A_SetRoll(random(0, 359));
			PIZZ A 10 A_Look;
			Loop;
		See:
			#### # 0 A_RemoveLight("hatered");
			PIZZ A 0 A_SetRoll(roll + 8);
			PIZZ A 2 A_Chase;
			Loop;
		Chill:
			PIZZ A 0 A_SetRoll(roll + 2);
			PIZZ A 2 A_Chase(null, null);
			PIZZ A 0 A_Jump(32, "See");
			Loop;
		Missile:
			PIZZ G 10 A_FaceTarget;
			#### # 0 A_AttachLightDef("hatered", "GLOWRED");
			PIZZ GGGGGG 1 BRIGHT A_PizzaAttack();
			#### # 0 A_RemoveLight("hatered");
			PIZZ G 20;
			PIZZ G 0 A_Jump(96, "Chill");
			goto See;
	}
}

Class PepperoniBullet : DI_Bullet
{
	Default
	{
		Radius 3;
		Height 6;
		Speed 12;
		DamageFunction (30);
		DI_Bullet.Trail "PIZZC0";
		DI_Bullet.TrailSize 6.0;
		DI_Bullet.FancyLight "";
	}
	
	States
	{
		Spawn:
			PIZZ C 0 NoDelay A_Jump(256, "Spinner");
	}
}

Class PizzaBlood : DI_Actor
{
	mixin DI_BoxBloodMixin;

	Default
	{
		+NOINTERACTION;
	}
	
	override void postbeginplay(void)
	{
		super.postbeginplay();
		DI_BoxGib("PIZZE0", 8);
		DI_BoxGib("PIZZF0", 5);
		DI_BoxGib("PIZZJ0", 24);
	}
	
	States
	{
		Spawn:
			TNT1 A 5 NoDelay A_ProjectileSmoke();
			Stop;
	}
}

Class DI_PizzaExplosion : DI_GibExplosion
{
	override void PostBeginPlay()
	{
		Super.PostBeginPlay();
		gibamount = 6;
	}

	override name GetGib(int gibnum) {return "DI_PizzaGib";}
	
	States
	{
		Spawn:
			TNT1 A 0 NoDelay A_SpawnItemEX("SafeBoom", flags: SXF_NOCHECKPOSITION);
			TNT1 A 10 StartGibbing();
			Stop;
	}
}

Class DI_PizzaGib : DI_Gib
{
	States
	{
		Roller:
			#### # 0 GibStart();
			PIZZ I 15;
			#### # 0 A_Jump(256, "Looper");
			Stop;
	}
}