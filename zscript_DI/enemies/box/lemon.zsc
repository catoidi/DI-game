//zscript_DI/enemies/box/lemon.zsc

Class Lemon : DI_Monster
{
	Default
	{
		Speed 10;
		Health 75;
		Radius 16;
		Height 40;
		Mass 240;
		DamageFactor "Melee", 1.5;
		DamageFactor "Lightning", 1.2;
		DI_Monster.GibType "DI_LemonExplosion";
		BloodType "DI_LemonBlood";
		BloodColor "Yellow";
		Tag "Kyle";
		Obituary "$BOX_DIE_LEMON";
		+FLOORCLIP;
		+NOINFIGHTSPECIES;
		+DONTHARMSPECIES;
		//$Category "Box Party/Enemies/"
		//$Title "Lemon"
		//$Color 4
	}
	
	States
	{
		Spawn:
			LEMN A 10 A_Look;
			Loop;
		See:
			LEMN AABBBB 2 A_Chase;
			Loop;
		Chill:
			LEMN AABBBB 2 A_Chase(null, null);
			Goto See;
		Missile:
			LEMN # 0 A_FaceTarget;
			LEMN DE 2;
			LEMN D 0 A_DIScale(-0.05, 0.05);
			LEMN DE 2;
			LEMN D 0 A_DIScale(-0.05, 0.05);
			LEMN DE 2;
			LEMN D 0 A_DIScale(-0.05, 0.05);
			LEMN DE 2;
			LEMN D 0 A_DIScale(-0.05, 0.05);
			LEMN DE 2;
			LEMN D 0 A_DIScale(-0.05, 0.05);
			LEMN F 2 A_FaceTarget;
			LEMN FFFFF 1 
			{
				A_StartSound("whiff", pitch: 1.5);
				A_SpawnProjectile("LemonBullet", frandom(24, 32), frandom(-4, 4), frandom(-2, 2), 0, frandom(8, 16));
				A_DIScale(0.1, -0.1);
				for (int i = 0; i < 4; ++i)
				{
					A_SpawnParticle("f8ff86", SPF_FULLBRIGHT | SPF_RELVEL, 48, 6, random(0, 359), 0, 0, 16, random(0, 8), 0, random(0, 4), 0, 0, -0.33, 1.0, -1, -0.15);
				}
			}
			LEMN DDDDD 1 A_DIScale(-0.05, 0.05);
			LEMN D 0 A_Jump(128, "Chill");
			Goto See;
	}
}

Class LemonBullet : DI_Bullet 
{
	Default
	{
		Gravity 0.3;
		Radius 4;
		Height 8;
		Speed 15;
		DamageFunction (3);
		DI_Bullet.Trail "LEMNN0";
		DI_Bullet.TrailSize 40.0;
		DI_Actor.IgnoreWeapon true;
		DamageType "Enemy";
		BounceType "Doom";
		BounceFactor 0.8;
		WallBounceFactor 0.25;
		BounceCount 2;
		Projectile;
		-NOGRAVITY;
		+FORCEXYBILLBOARD;
	}

	States
	{
		Spawn:
			LEMN N 0 NoDelay A_Jump(256, "Spinner");
		Death:
			LEMN N 0 BRIGHT A_StartSound("piebomb", volume: 0.1, pitch: 1.2);
			LEMN NNNO 2 BRIGHT;
			LEMN N 0 BRIGHT A_StartSound("piebomb", volume: 0.1, pitch: 1.2);
			LEMN NNNO 2 BRIGHT;
			LEMN N 0 BRIGHT A_StartSound("piebomb", volume: 0.1, pitch: 1.2);
			LEMN NNNO 2 BRIGHT;
			TNT1 A 0 A_Explode(6, 48, XF_CIRCULAR, fulldamagedistance: 48);
			TNT1 A 0 A_SpawnItemEX("SafeBoomSmall", flags: SXF_NOCHECKPOSITION);
			//TNT1 A 0 A_SpawnItemEX("DangerBoomLemon", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERPOINTERS | SXF_NOCHECKPOSITION);
			//TNT1 AA 0 A_SpawnItemEx("LemonChunk", random(-2, 2), random(-2, 2), random(2, 4), random(-6, 6), random(-6, 6), random(2, 8), random(0, 359), SXF_NOCHECKPOSITION, 128);
			//TNT1 A 0 A_SpawnItemEx("LemonGusher", random(-2, 2), random(-2, 2), random(2, 4), random(-2, 2), random(-2, 2), random(1, 3), random(0, 359), SXF_NOCHECKPOSITION);
			//TNT1 AA 0 A_SpawnParticle("f8ff86", SPF_FULLBRIGHT | SPF_RELVEL, 48, 6, random(0, 359), 0, 0, 2, random(0, 8), 0, random(0, 4), 0, 0, -0.33, 1.0, -1, -0.15);
			TNT1 A 5;
			Stop;
	}
}

CLASS DI_LemonBlood : DI_Blood
{
	mixin DI_BoxBloodMixin;

	Default
	{
		Alpha 0.5;
	}
	
	override void postbeginplay(void)
	{
		super.postbeginplay();
		DI_BoxGib("LEMNM0", 40);
		DI_BoxGib("LEMNM0", 40);
		DI_BoxGib("LEMNM0", 40);
	}
	
	States
	{
		Roller:
			#### # 0 
			{
				self.Scale.X *= frandom(0.5, 1.2); 
				self.Scale.Y *= frandom(0.5, 1.2);
				A_SetRoll(random(0, 359));
			}
			BLDY A 15 A_SpawnItemEX("DI_BloodPuff", flags: SXF_NOCHECKPOSITION | SXF_TRANSFERTRANSLATION | SXF_TRANSFERSPRITEFRAME | SXF_TRANSFERSCALE | SXF_TRANSFERALPHA | SXF_TRANSFERRENDERSTYLE | SXF_TRANSFERROLL);
		Looper:
			#### # 15 {A_FadeOut(0.2); A_DIScale(1.2, 1.2, true);}
			Loop;
	}
}

Class DI_LemonBlood_Gib : DI_BloodPuff_Gib
{
	Default
	{
		Alpha 0.5;
	}

	States
	{
		Spawn:
			TNT1 A 1;
			BLDY A 1 DoGibPuff(0);
	}
}

Class DI_LemonExplosion : DI_GibExplosion
{
	override void postbeginplay()
	{
		super.postbeginplay();
		gibamount = 3;
		randomblood = 0;
	}
	
	override name GetGib(int gibnum) 
	{
		switch(gibnum)
		{
			case 0: return "DI_LemonGib"; break;
			case 1: return "DI_LemonGib2"; break;
			default: return "DI_LemonGib3"; break;
		}
		return "";
	}

	States
	{
		Spawn:
			TNT1 A 0 NoDelay StartGibbing(false);
			BLDY A 1 GibAway();
			Wait;
	}
}

Class DI_LemonGib : DI_GibBloody
{
	Default
	{
		DI_GibBloody.TrailActor "DI_LemonBlood_Gib";
	}

	States
	{
		Roller:
			#### # 0 GibStart();
			LEMN H 15;
			#### # 0 A_Jump(256, "Looper");
			Stop;
	}
}

Class DI_LemonGib2 : DI_LemonGib
{
	States
	{
		Roller:
			#### # 0 GibStart();
			LEMN I 15;
			#### # 0 A_Jump(256, "Looper");
			Stop;
	}
}

Class DI_LemonGib3 : DI_Gib
{
	States
	{
		Roller:
			#### # 0 GibStart();
			LEMN J 15 {frame += random(0, 1);}
			#### # 0 A_Jump(256, "Looper");
			Stop;
	}
}