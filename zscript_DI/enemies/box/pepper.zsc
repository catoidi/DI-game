//zscript_DI/enemies/box/pepper.zsc

Class Pepper : DI_Monster
{
	Default
    {
        Speed 10;
        Radius 32;
        Height 64;
        Health 100;
        Mass 250;
		DamageFactor "Ice", 1.5;
		DamageFactor "FakeIce", 1.5;
		DamageFactor "Fire", 0.75;
		DamageFactor "FakeFire", 0.75;
        BloodType "DI_PepperBlood";
		DI_Monster.GibType "DI_PepperExplosion";
		DI_Monster.DIDrop "DI_Money";
		Species "Pepper";
        Tag "Jalape√±o";
		Obituary "$BOX_DIE_PEPPER";
        +NOBLOODDECALS;
		+FLOORCLIP;
		+JUMPDOWN;
		-DONTHARMSPECIES;
		//$Category "Box Party/Enemies/"
		//$Title "Pepper"
		//$Color 4
    }
	
	States
    {
        Spawn:
			PEPR A 12 A_Look;
			Loop;
		See:
			PEPR AABB 5 A_Chase;
			Loop;
		Chill:
			PEPR AABB 5 A_Chase(null, null);
			Goto See;
        Missile:
			PEPR C 0 A_FaceTarget;
			PEPR C 0 A_StartSound("pepload1");
            PEPR CCCC 4 Slow A_DIScale(-0.0125, 0.025);
			PEPR D 0 A_FaceTarget;
			PEPR D 0 A_StartSound("pepload2");
            PEPR DDDD 4 Slow A_DIScale(0.0125, -0.025);
            PEPR D 0 A_FaceTarget;
			PEPR E 0 BRIGHT A_StartSound("peppow");
            PEPR E 6 BRIGHT A_SpawnProjectile("PepperBullet", 16, 0, 0);
            PEPR D 6 A_FaceTarget;
			PEPR E 0 BRIGHT A_StartSound("peppow");
            PEPR E 6 BRIGHT A_SpawnProjectile("PepperBullet", 16, 0, 0);
            PEPR D 6;
			PEPR D 0 A_Jump(128, "Chill");
            Goto See;
		XDeath:
			#### # 0 A_ResetScale();
			PEPR G 1 A_FaceTarget;
			PEPR G 1 A_Recoil(3);
			PEPR G 4 ThrustThingZ(0, 15, 0, 1);
			#### # 0 A_DITargetPlayer();
			#### # 0 A_DIDie();
			#### ### 0 A_DIDieBlood();
			#### # 0 A_DIGibExplode();
			#### # 0 A_DISpawnBloodCoins();
			PEPR H 10;
			#### # 0 A_RemoveLight("deathlight");
			PEPR I 175;
			PEPR I 0 A_Jump(256, "DeathLoop");
	}
}

Class PepperBullet : DI_Bullet
{
	Default
	{
		Radius 12;
		Height 24;
		Speed 16;
		DamageFunction (10);
		DI_Bullet.Trail "PEPRP0";
		DI_Bullet.FancyLight "GLOWYELLOW";
	}
	
	States
	{
		Spawn:
			PEPR PPPPQQQQ 1 DIBulletTrail();
			Loop;
		Death:
			#### # 0 A_SpawnItemEX("SafeBoom", 0, 12, flags: SXF_NOCHECKPOSITION);
			#### # 0 A_Explode(10, 64, XF_CIRCULAR, fulldamagedistance: 64);
		DeathLoop:
			#### # 0 A_RemoveLight("fancylight");
			#### # 1 A_FadeOut(0.4);
			Loop;
	}
}

CLASS DI_PepperBlood : DI_Actor
{
	mixin DI_BoxBloodMixin;

	Default
	{
		+NOINTERACTION;
	}
	
	override void postbeginplay(void)
	{
		super.postbeginplay();
		DI_BoxGib("PEPRN0", 64);
		DI_BoxGib("PEPRN0", 64);
		DI_BoxGib("PEPRO0", 64);
	}
	
	States
	{
		Spawn:
			TNT1 A 5 NoDelay A_ProjectileSmoke();
			Stop;
	}
}

Class DI_PepperExplosion : DI_GibExplosion
{	
	override void PostBeginPlay()
	{
		Super.PostBeginPlay();
		gibamount = 4;
	}

	override name GetGib(int gibnum) 
	{
		switch(gibnum)
		{
			case 1: return "DI_PepperGib1"; break;
			case 2: return "DI_PepperGib2"; break;
			case 3: return "DI_PepperGib3"; break;
			case 4: return "DI_PepperGib4"; break;
		}
		return "";
	}

	States
	{
		Spawn:
			TNT1 A 10 NoDelay StartGibbing();
			Stop;
	}
}


Class DI_PepperGib1 : DI_Gib
{
	States
	{
		Roller:
			#### # 0 GibStart();
			PEPR J 15;
			#### # 0 A_Jump(256, "Looper");
			Stop;
	}
}

CLASS DI_PepperGib2 : DI_Gib
{
	States
	{
		Roller:
			#### # 0 GibStart();
			PEPR K 15;
			#### # 0 A_Jump(256, "Looper");
			Stop;
	}
}

CLASS DI_PepperGib3 : DI_Gib
{
	States
	{
		Roller:
			#### # 0 GibStart();
			PEPR L 15;
			#### # 0 A_Jump(256, "Looper");
			Stop;
	}
}

CLASS DI_PepperGib4 : DI_Gib
{
	States
	{
		Roller:
			#### # 0 GibStart();
			PEPR M 15;
			#### # 0 A_Jump(256, "Looper");
			Stop;
	}
}