//zboxscript/enemies/fruitpuncher.zsc

//its a watermelon that punches ya.

CLASS FruitPuncher : DI_Monster //20007
{
	Default
	{
		Speed 20;
		Health 40;
		Radius 16;
		Height 40;
		Mass 130;
		BloodType "DI_FruitBlood";
		Species "Melon";
		Tag "Fruit Puncher";
		Obituary "$BOX_DIE_FRUIT";
		DI_Monster.GibType "DI_FruitExplosion";
		+FLOORCLIP;
		+JUMPDOWN;
		//$Category "Box Party/Enemies/"
		//$Title "Melon"
		//$Color 4
	}
	
	States
	{
		Spawn:
			MELN A 10 A_Look;
			Loop;
		See:
			MELN AABB 3 A_Chase;
			Loop;
		Melee:
			#### C 0 A_FaceTarget;
			#### CC 5 A_DIScale(-0.025, 0.05);
			#### D 0 A_Recoil(-10);
			#### D 0 A_CustomMeleeAttack(6, "melee", "whiff", "Melee", false);
			#### D 10 A_DIScale(0.05, -0.1);
			Goto See;
	}
}

CLASS DI_FruitBlood : DI_Blood
{
	mixin DI_BoxBloodMixin;

	Default
	{
		Alpha 1.0;
	}
	
	override void postbeginplay(void)
	{
		super.postbeginplay();
		DI_BoxGib("MELNP0", 10);
		DI_BoxGib("MELNP0", 10);
		DI_BoxGib("MELNP0", 10);
	}
	
	States
	{
		Roller:
			#### # 0 
			{
				frame = random(0, 3);
				self.Scale.X *= frandom(0.5, 1.2); 
				self.Scale.Y *= frandom(0.5, 1.2);
				A_SetRoll(random(0, 359));
			}
			BLDR # 15 A_SpawnItemEX("DI_BloodPuff", flags: SXF_NOCHECKPOSITION | SXF_TRANSFERTRANSLATION | SXF_TRANSFERSPRITEFRAME | SXF_TRANSFERSCALE | SXF_TRANSFERALPHA | SXF_TRANSFERRENDERSTYLE | SXF_TRANSFERROLL);
		Looper:
			#### # 15 {A_FadeOut(0.2); A_DIScale(1.2, 1.2, true);}
			Loop;
	}
}

Class DI_FruitBlood_Gib : DI_BloodPuff_Gib
{
	States
	{
		Spawn:
			TNT1 A 1;
			BLDR A 1 DoGibPuff(3);
	}
}

Class DI_FruitExplosion : DI_GibExplosion
{
	override void postbeginplay()
	{
		super.postbeginplay();
		
		randomblood = random(0, 3);
	}
	
	override name GetGib(int gibnum) 
	{
		switch(gibnum)
		{
			case 0:
			case 1: return "DI_FruitGib"; break;
			default: return "DI_FruitGib2"; break;
		}
		return "DI_FruitGib";
	}

	States
	{
		Spawn:
			TNT1 A 0 NoDelay StartGibbing();
			BLDR A 1 GibAway(invoker.randomblood);
			Wait;
	}
}

Class DI_FruitGib : DI_GibBloody
{
	Default {DI_GibBloody.TrailActor "DI_FruitBlood_Gib";}

	States
	{
		Roller:
			#### # 0 GibStart();
			MELN J 15;
			#### # 0 A_Jump(256, "Looper");
			Stop;
	}
}

CLASS DI_FruitGib2 : DI_Gib
{
	States
	{
		Roller:
			#### # 0 GibStart();
			MELN J 15 {frame = random(10, 14);}
			#### # 0 A_Jump(256, "Looper");
			Stop;
	}
}