//zscript_DI/enemies/box/smallpizza.zsc

Class PizzaSlicer : Pizzaulter
{
	Default
	{
		Speed 4;
		Health 10;
		Radius 16;
		Height 32;
		Mass 60;
		DamageFactor "Melee", 1.5;
		DamageFactor "Poison", 1.25;
		DamageFactor "FakePoison", 1.25;
		DamageFunction (3);
		DI_Monster.GibType "DI_SmallPizzaExplosion";
		DI_Monster.DIDropNum 1;
		Tag "Pizza Slicer";
		Obituary "$BOX_DIE_PIZZA_SMALL";
		-DONTHARMSPECIES;
		//$Category "Box Party/Enemies/"
		//$Title "Pizza (Small)"
		//$Color 4
	}
	
	States
	{
		Spawn:
			PIZZ B 0 NoDelay A_SetRoll(random(0, 359));
			#### # 0 A_AttachLightDef("hatered", "GLOWRED");
			PIZZ B 10 BRIGHT A_Look;
			Loop;
		See:
			PIZZ B 2 BRIGHT
			{
				A_StopSound(CHAN_BODY);
				A_SetRoll(roll + 2);
				A_Chase();
			}
			Loop;
		NoMissile:
			PIZZ BBBBBBBB 1
			{
				A_StopSound(CHAN_BODY);
				A_SetRoll(roll + 2);
				A_Wander();
			}
			Goto See;
		Missile:
			PIZZ H 0 BRIGHT A_StopSound(CHAN_BODY);
			PIZZ H 0 BRIGHT A_FaceTarget;
			PIZZ H 0 BRIGHT
			{
				if (BoxHammerCheck() == true) {SetStateLabel("NoMissile", false);}
			}
			PIZZ H 10 BRIGHT A_FaceTarget;
			PIZZ H 1 A_SkullAttack;
			PIZZ H 3 {self.Vel = ((self.Vel.X / 1.5), (self.Vel.Y / 1.5), (self.Vel.Z / 1.5));}
		Charger:
			PIZZ B 0 A_StartSound("pwoosh", CHAN_BODY, CHANF_NOSTOP);
			PIZZ B 0 A_SpawnParticle("cd 86 50", SPF_FULLBRIGHT | SPF_RELVEL, 48, 8, random(0, 359), 0, 0, 8, random(0, 8), 0, random(0, 4), 0, 0, -0.33, 1.0, -1, -0.15);
			PIZZ BBBB 1 A_SetRoll(roll - 12);
			Loop;
	}
}