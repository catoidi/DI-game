//zscript_DI/enemies/box/onion.zsc

Class OnionTurret : DI_Monster
{
	Default
	{
		Speed 0;
		Health 1440;
		Radius 30;
		Height 56;
		Mass 0x7FFFFFFF; //immovable object
		PainChance 64;
		DamageFactor "Melee", 1.5;
		DamageFactor "Lightning", 1.25;
		DamageFactor "FakeLightning", 1.25;
		BloodType "OnionBlood";
		DI_Monster.GibType "DI_OnionExplosion";
		DI_Monster.DIDrop "DI_Money";
		MinMissileChance 1;
		MeleeThreshold 0;
		Tag "Henry";
		Obituary "$BOX_DIE_ONION";
		+BRIGHT;
		-PUSHABLE;
		+MOVEWITHSECTOR;
		+NOINFIGHTING;
		+NOBLOODDECALS;
		+DONTHARMSPECIES;
		//$Category "Box Party/Enemies/"
		//$Title "Onion (Big)"
		//$Color 4
	}
	
	override void PostBeginPlay()
	{
		Super.PostBeginPlay();
		
		A_AttachLight("onionlight", DynamicLight.PointLight, "White", radius, radius, DYNAMICLIGHT.LF_ATTENUATE, (0, 0, height / 2));
	}
	
	override void StatusTick()
	{
		Super.StatusTick();
		
		if (health <= 0) {A_RemoveLight("onionlight");}
	}
	
	action void A_OnionAttack (int Angled, int Offseted)
	{
		//Shoots 8 water projectiles in a circle around the calling actor.
		for (int i = 0; i < 8; ++i)
		{
			A_SpawnParticle("00efff", SPF_FULLBRIGHT | SPF_RELVEL, 48, 8, random(0, 359), 0, 0, 16, random(0, 16), 0, random(0, 8), 0, 0, -0.33, 1.0, -1, -0.15);
		}
		A_StartSound("onion2");
		A_SpawnProjectile("OnionBullet", 32, 0, Angled, CMF_OFFSETPITCH, Offseted);
		A_SpawnProjectile("OnionBullet", 32, 0, (Angled + 45), CMF_OFFSETPITCH, Offseted);
		A_SpawnProjectile("OnionBullet", 32, 0, (Angled + 90), CMF_OFFSETPITCH, Offseted);
		A_SpawnProjectile("OnionBullet", 32, 0, (Angled + 135), CMF_OFFSETPITCH, Offseted);
		A_SpawnProjectile("OnionBullet", 32, 0, (Angled + 180), CMF_OFFSETPITCH, Offseted);
		A_SpawnProjectile("OnionBullet", 32, 0, (Angled + 225), CMF_OFFSETPITCH, Offseted);
		A_SpawnProjectile("OnionBullet", 32, 0, (Angled + 270), CMF_OFFSETPITCH, Offseted);
		A_SpawnProjectile("OnionBullet", 32, 0, (Angled + 315), CMF_OFFSETPITCH, Offseted);
		//A_BoxParticle("00 ef ff", 12, 24, 60, PBright: true);
	}
	
	States
	{
		Spawn:
			ONIO A 10 A_Look;
			Loop;
		See:
			ONIO A 0 A_Jump(128, "Chill");
			Goto See2;
		See2:
			ONIO AB 8 A_Chase;
			Loop;
		Chill:
			ONIO AB 8;
			Goto See2;
		Missile:
			ONIO C 0 A_RemoveLight("Shocking");
			ONIO C 0 A_FaceTarget;
			ONIO CCC 5 A_DIScale(-0.05, 0.05);
			ONIO D 0 A_OnionAttack(0, 0);
			ONIO DDD 3 A_DIScale(0.1, -0.1);
			ONIO D 1 A_Jump(96, "Missile2");
			ONIO BBB 3 A_DIScale(-0.05, 0.05);
			Goto See;
		Missile2:
			ONIO C 0 A_FaceTarget;
			ONIO CCCC 5 A_DIScale(-0.05, 0.05);
			ONIO D 0 A_OnionAttack(0, 0);
			ONIO DDD 3 A_DIScale(0.1, -0.1);
			ONIO D 1 A_Jump(64, "Missile3");
			ONIO BBBB 3 A_DIScale(-0.0625, 0.0625);
			Goto See;
		Missile3:
			ONIO C 0 A_FaceTarget;
			ONIO CCCCC 5 A_DIScale(-0.05, 0.05);
			ONIO D 0 A_OnionAttack(0, 0);
			ONIO DDD 3 A_DIScale(0.1, -0.1);
			ONIO D 1 A_Jump(32, "Missile4");
			ONIO BBBBB 3 A_DIScale(-0.06, 0.06);
			Goto See;
		Missile4:
			ONIO C 0 A_FaceTarget;
			ONIO CCCCCC 5 A_DIScale(-0.05, 0.05);
			ONIO D 0 A_OnionAttack(0, 0);
			ONIO DDD 3 A_DIScale(0.15, -0.15);
			ONIO BBBBBB 3 A_DIScale(-0.075, 0.075);
			Goto See;
		XDeath:
			#### # 0 A_ResetScale();
			ONIO E 1 A_FaceTarget;
			ONIO E 1 A_Recoil(3);
			ONIO E 4 ThrustThingZ(0, 15, 0, 1);
			#### # 0 A_DITargetPlayer();
			#### # 0
			{
				A_OnionAttack(0, 0);
				A_OnionAttack(15, -10);
				A_OnionAttack(30, -20);
			}
			#### # 0 A_DIDie();
			#### ### 0 A_DIDieBlood();
			#### # 0 A_DIGibExplode();
			#### # 0 A_DISpawnBloodCoins();
			ONIO F 10;
			#### # 0 A_RemoveLight("deathlight");
			ONIO G 175;
			ONIO G 0 A_Jump(256, "DeathLoop");
	}
}

Class OnionBullet : DI_Bullet
{
	Default
	{
		Radius 4;
		Height 8;
		Speed 10;
		DamageFunction (60);
		DI_Bullet.Trail "ONIOJ0";
		DI_Bullet.TrailSize 10.0;
		DI_Bullet.FancyLight "GLOWCYAN";
	}
	
	States
	{
		Spawn:
			ONIO J 0 NoDelay A_Jump(256, "Spinner");
	}
}

CLASS OnionBlood : DI_Actor
{
	mixin DI_BoxBloodMixin;

	Default
	{
		+NOINTERACTION;
	}
	
	override void postbeginplay(void)
	{
		super.postbeginplay();
		DI_BoxGib("ONIOI0", 6);
		DI_BoxGib("ONIOI0", 6);
		DI_BoxGib("ONIOH0", 24);
	}
	
	States
	{
		Spawn:
			TNT1 A 5 NoDelay A_ProjectileSmoke();
			Stop;
	}
}

Class DI_OnionExplosion : DI_Actor
{
	Default
	{
		+NOINTERACTION;
	}

	States
	{
		Spawn:
			TNT1 AAAA 0 NoDelay A_SpawnItemEX("OnionBlood", random(-12, 12), random(-12, 12), random(12, 40), flags: SXF_NOCHECKPOSITION | SXF_TRANSFERTRANSLATION);
			TNT1 A 0 A_SpawnItemEX("SafeSplash", 0, 0, 16, flags: SXF_NOCHECKPOSITION);
			TNT1 A 5;
			Stop;
	}
}