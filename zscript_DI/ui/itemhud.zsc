//zscript_DI/handlers/itemhud.zsc

class DIItemHUDHandler : EventHandler
{
	Array<String> filedata; //all the lines from DISHOP.

	transient ui cvar itemscale, itemhudtoggle, itemalpha, photomode;
	ui DI_PlayerBase diplr;
	ui Array<String> itemnames;
	ui Array<String> itemsprites;
	ui Array<String> validitems;
	ui Array<String> validsprites;
	ui Array<Int> validcount;
	ui Array<Int> validperm;
	ui bool heartfade;
	ui int heartfade_int;
	
	override void WorldLoaded(WorldEvent e)
	{	
		int ThisLump = Wads.FindLump("DISHOP", 0, Wads.ANYNAMESPACE);
		int NextLump = Wads.FindLump("DISHOP", ThisLump + 1, Wads.ANYNAMESPACE);
		
		while (ThisLump != -1)
		{
			Wads.ReadLump(ThisLump).Split(filedata, "\n", TOK_SKIPEMPTY);
			ThisLump = NextLump;
			NextLump = Wads.FindLump("DISHOP", ThisLump + 1, 1);
		}
		
		for (int i = 0; i < filedata.Size(); i++)
		{
			if (filedata[i].Left(2) == ">>") {continue;} //dont bother reading the next lines of code if it's a comment (starts with ">>")
			if (filedata[i].Length() < 3) {continue;}
			filedata[i] = filedata[i].Left(filedata[i].IndexOf(";"));
			
			Array<String> currentline;
			filedata[i].Split(currentline, " ");
			if (currentline.Size() < 4) {continue;} //this is not an item. skipped.
			let inttxt = String.Format("diitemhud %s %s", currentline[1], currentline[2]);
			EventHandler.SendInterfaceEvent(consoleplayer, inttxt); //send the item name and sprite to the ui scope, for use with HUD elements.
		}
	}
	
	override void InterfaceProcess(ConsoleEvent e)
	{
		if (e.Name.Left(10) == "diitemhud ")
		{
			Array<String> currentline;
			e.Name.Split(currentline, " ");
			if (itemnames.Find(currentline[1]) == itemnames.Size()) //not a duplicate, add to the list of shop items.
			{
				itemnames.Push(currentline[1]); //create a list of all valid shop item names and sprites.
				itemsprites.Push(currentline[2]);
			}
		}
	}
	
	override void UITick()
	{
		diplr = DI_PlayerBase(players[consoleplayer].mo);
		if (!diplr) {return;} //no player = no inventory!
		itemhudtoggle = cvar.GetCVAR("di_itemhud_toggle", players[consoleplayer]);
		itemscale = cvar.GetCVAR("di_itemhud_scale", players[consoleplayer]);
		itemalpha = cvar.GetCVAR("di_itemhud_alpha", players[consoleplayer]);
		photomode = cvar.getcvar("di_photomode", players[consoleplayer]);
		if (!itemhudtoggle) {return;}
		if (!itemscale) {return;}
		if (!photomode) {return;}
		validitems.Clear();
		validsprites.Clear();
		validcount.Clear();
		validperm.Clear();
		if (itemhudtoggle.GetBool() == false) {return;}
		for (int i = 0; i < itemnames.Size(); i++)
		{
			inventory curitem = diplr.FindInventory(itemnames[i], false); //search player's inventory.
			if (curitem is "DIPassiveInventory") //Only look for Passive items. No weapons, no consumables, etc.)
			{
				DIPassiveInventory hiddencheck = DIPassiveInventory(curitem);
				if (hiddencheck.dipassive_hidden == false)
				{
					validitems.Push(itemnames[i]);
					validsprites.Push(itemsprites[i]);
					validcount.Push(diplr.CountInv(itemnames[i]));
					bool hasperma = false;
					for (int j = 0; j < diplr.wavekeep.Size(); ++j) //horrible. no good.
					{
						if (hasperma == true) {continue;}
						if (itemnames[i].MakeLower() == diplr.wavekeep[j].Left(itemnames[i].Length()).MakeLower()) {hasperma = true;}
					}
					if (hasperma == true) {validperm.Push(1);}
					else {validperm.Push(0);}
				}
			}
		}
		
		if (heartfade == true)
		{
			++heartfade_int;
			if (heartfade_int >= 35) {heartfade = false;}
		}
		else
		{
			--heartfade_int;
			if (heartfade_int <= 0) {heartfade = true;}
		}
	}
	
	override void RenderOverlay(RenderEvent e)
	{
		for (int i = 0; i < validitems.Size(); i++)
		{
			if (photomode.GetBool() == true) {return;}
			if (!itemscale) {return;}
			let scalenum = itemscale.GetFloat();
			let alphanum = itemalpha.GetFloat();
			let realsprite = TexMan.CheckForTexture(String.Format("%s0", validsprites[i])); //gross hack
			if (realsprite)
			{
				let limitX = Screen.GetWidth() / 3;
				let singleX = (24 * CleanXFac * scalenum);
				int Xmax = limitX / singleX;
				int itemrows = i / Xmax;
				let startx = Screen.GetWidth() - (singleX * ((i % Xmax) + 0.5));
				let starty = (24 * (1 + itemrows)) * CleanYFac * scalenum;
				Screen.DrawTexture(realsprite, false, startx, starty,
					DTA_ScaleX, scalenum * CleanXFac,
					DTA_ScaleY, scalenum * CleanYFac,
					DTA_Alpha, alphanum,
					DTA_KeepRatio, true
				);
				if (validperm[i] == 1)
				{
					Screen.DrawTexture(TexMan.CheckForTexture("PUFFH0"), false,
						startx - (8 * CleanXFac * scalenum), 
						starty - (20 * CleanYFac * scalenum),
						DTA_ScaleX, scalenum * CleanXFac,
						DTA_ScaleY, scalenum * CleanYFac,
						DTA_Alpha, alphanum * (heartfade_int / 35.0),
						DTA_KeepRatio, true
					);
				}
				if (validcount[i] > 1)
				{
					int itemcount = validcount[i];
					startx += 4 * CleanXFac * scalenum;
					starty -= 8 * CleanYFac * scalenum;
					if (itemcount >= 10) {startx -= 8 * CleanXFac * scalenum;}
					if (itemcount >= 100) {startx -= 8 * CleanXFac * scalenum;}
					if (itemcount > 999) {itemcount = 999;}
					Screen.DrawText(smallfont, Font.CR_GOLD, startx, starty, String.Format("%d", validcount[i]),
						DTA_ScaleX, scalenum * CleanXFac,
						DTA_ScaleY, scalenum * CleanYFac,
						DTA_Alpha, alphanum,
						DTA_KeepRatio, true
					);
				}
			}
		}
	}
}