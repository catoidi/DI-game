//zscript_DI/items/consumables/tomahawk.zsc

Class DI_Tomahawk : DI_ThrowBomb
{
	Default
	{
		Tag "Tomahawk";
		DICustomConsumable.CooldownTics 105;
		Inventory.MaxAmount 24;
		Inventory.Icon "HAWKA0";
		Inventory.PickupMessage "Got a throwable tomahawk.";
	}
	
	States
	{
		Spawn:
			HAWK A -1;
			Loop;
		Use:
			HAWK A 0 {A_FireProjectile("DI_Tomahawk_Projectile", 0, 0, 0, 0, FPF_NOAUTOAIM);}
			Stop;
	}
}

Class DI_Tomahawk_Projectile : DI_ThrowBomb_Projectile
{
	actor hearted;

	Default
	{
		DamageFunction (5120);
		+NOGRAVITY;
		+BLOODSPLATTER;
		//+EXTREMEDEATH;
	}
	
	override int SpecialMissileHit(Actor victim)
	{
		if (victim && (!target || victim != target) && victim.bSHOOTABLE)
		{
			hearted = victim;
			return -1;
		}
		else return 1;
	}
	
	action void TomahawkParticle()
	{
		A_SpawnParticleEX("White", TexMan.CheckForTexture("HAWKA0"), 0, SPF_ROLL, 24, 32.0, random(0, 359), 0, 0, 0, random(-1, 1), random(-1, 1), random(1, 3), 0, 0, -0.33, 0.5, 0, -0.16, random(0, 359), 3 - (6 * (random(0, 1))));
	}
	
	States
	{
		Spawn:
			HAWK A 1 A_SetRoll(roll - 12);
			Wait;
		Death:
			HAWK A 1 A_ProjectileSmoke();
			TNT1 A 5 A_SpawnItemEX("DI_Tomahawk", 0, 0, 0, random(-2, 2), random(-2, 2), random(4, 6), flags: SXF_NOCHECKPOSITION);
			Stop;
		XDeath:
		Crash:
			TNT1 A 1
			{
				A_ProjectileSmoke();
				TomahawkParticle();
			}
			#### # 5
			{
				if (hearted && hearted.health < 1)
				{
					for (int i = 0; i < 6; ++i)
					{
						A_SpawnItemEX("DI_BloodyHeart_Drop", 0, 0, 0, random(-3, 3), random(-3, 3), random(2, 6), 0, SXF_ABSOLUTEVELOCITY | SXF_NOCHECKPOSITION);
						if (hearted.bNOBLOOD == false)
						{
							hearted.BloodSplatter(invoker.Pos, invoker.Angle);
						}
					}
					eventhandler.sendnetworkevent("di_tomahawkkill");
				}
			}
			Stop;
	}
}

Class DI_Tomahawk_Bundle : DIBundle
{
	Default
	{
		Tag "Handful of Tomahawks";
		DIBundle.BundleItem "DI_Tomahawk";
		DIBundle.BundleAmount 8;
		Inventory.PickupMessage "Got a handful of tomahawks, pre-coated in blood for your convenience.";
	}
	
	States
	{
		Spawn:
			HAWK B -1;
			Stop;
	}
}