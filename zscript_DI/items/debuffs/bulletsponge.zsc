//zscript_DI/items/debuffs/bulletsponge.zsc

Class DI_QuestDebuff_BulletSponge : DI_QuestDebuff
{
    default 
	{
		Tag "\cgCurse (Bullet Sponge)";
	}

    override string, int GiveEnemyItem()
    {
        if (owner) {return "DI_BulletSponge_Buff", owner.CountInv(GetClassName());}
        else return "", 0;
    }
}

Class DI_BulletSponge_Buff : Inventory
{
    default
    {
        inventory.maxamount 999999;
    }

    int healnum;
    int healtime;

    override void AbsorbDamage(int damage, Name damageType, out int newdamage, Actor inflictor, Actor source, int flags)
    {
        if (owner)
        {
            if (source != self)
            {
                healnum += (newdamage * owner.CountInv(GetClassName())) / 2;
            }
        }
        Super.AbsorbDamage(damage, damageType, newdamage, inflictor, source, flags);
    }

    override void DoEffect()
    {
        if (owner && (healnum > 0) && (owner.health > 0))
        {
            ++healtime;
            if (healtime >= 30)
            {
                int healamount = healnum / 2;
                
                for (int i = 0; i < healamount; ++i)
                {
                    owner.A_SpawnParticle("6be9ee", SPF_FULLBRIGHT, 30, 5, 0, random(-owner.radius, owner.radius), random(-owner.radius, owner.radius), random(0, owner.height), 0, 0, 0, 0, 0, 0.33);
                }

                owner.GiveBody(healamount);
                healnum -= healamount;
                healtime = 0;
            }
        }
        else {healnum = 0;}
    }
}