//zscript_DI/items/passives/statups.zsc

//if you're gonna make some new stat up items for DI, this is the "proper" way to do it.

Class DI_BigBullets : DIPassiveInventory
{
	Default
	{
		Tag "Big Bullets";
		Inventory.Icon "POWRR0";
		Inventory.PickupMessage "Acquired Big Bullets! (Increases DMG Mult, Bullet Size, and Recoil.)";
		Inventory.MaxAmount 10;
	}
	
	override double GiveDamageMult()
	{
		if (!plr) {return 0.0;}
		return 0.15 * plr.countinv(GetClassName());
	}

	override double GiveBulletSize()
	{
		if (!plr) {return 0.0;}
		return 0.3 * plr.countinv(GetClassName());
	}

	override double GiveRecoil()
	{
		if (!plr) {return 0.0;}
		return 2 * plr.countinv(GetClassName());
	}
	
	States
	{
		Spawn:
			POWR R -1;
			Stop;
	}
}

Class DI_ComboDamageMult : DIPassiveInventory
{
	Default
	{
		Tag "Combo DMG Mult";
		Inventory.Icon "POWRT0";
		Inventory.PickupMessage "Combo Damage Multiplier Acquired! (+10% DMG, -5 Speed per Combo)";
		Inventory.MaxAmount 3;
	}
	
	override double GiveDamageMult()
	{
		if (!plr) {return 0.0;}
		return (0.1 * (plr.combonum - 1)) * plr.countinv(GetClassName());
	}
	
	override double GivePlayerSpeed()
	{
		if (!plr) {return 0.0;}
		return (-0.05 * (plr.combonum - 1)) * plr.countinv(GetClassName());
	}
	
	States
	{
		Spawn:
			POWR T -1;
			Stop;
	}
}

Class DI_PainRhombus : DIPassiveInventory
{
	Default
	{
		Tag "Pain Rhombus";
		Inventory.Icon "POWRS0";
		Inventory.PickupMessage "Pain Rhombus Acquired! (+50% DMG, -10% per combo.)";
		Inventory.MaxAmount 3;
	}
	
	override double GiveDamageMult()
	{
		if (!plr) {return 0.0;}
		int painstarnum = plr.CountInv("DI_PainRhombus") * 6;
		painstarnum -= plr.CountInv("DI_PainRhombus") * plr.combonum;
		if (painstarnum < 0) {painstarnum = 0;}
		return 0.1 * painstarnum;
	}
	
	States
	{
		Spawn:
			POWR S -1;
			Stop;
	}
}

Class DI_GunHoarder : DIPassiveInventory
{
	Default
	{
		Tag "Gun Hoarder";
		Inventory.Icon "POWRU0";
		Inventory.PickupMessage "Gun Hoarder Acquired! (Gain +10% DMG for every weapon you own.)";
		Inventory.MaxAmount 3;
	}
	
	override double GiveDamageMult()
	{
		if (!plr) {return 0.0;}
		return (0.1 * plr.weaponnum) * plr.countinv("DI_GunHoarder");
	}
	
	States
	{
		Spawn:
			POWR U -1;
			Stop;
	}
}

Class DI_RewardsProgram : DIPassiveInventory
{
	Default
	{
		Tag "Rewards Program";
		Inventory.Icon "POWRW0";
		Inventory.PickupMessage "Acquired Rewards Program! (Up to +3 coins per enemy, but only while in lots of debt.)";
		Inventory.MaxAmount 3;
	}
	
	override int GiveExtraCoin()
	{
		if (!plr) {return 0;}
		int addcoin = plr.countinv("DI_RewardsProgram");
		if (plr.score <= -2000) {addcoin *= 3;}
		else if (plr.score <= -1000) {addcoin *= 2;}
		return addcoin;
	}
	
	States
	{
		Spawn:
			POWR W -1;
			Stop;
	}
}

Class DI_FastWeapon : DIPassiveInventory
{
	default
	{
		inventory.maxamount 5;
	}
	
	override double GiveWeaponSpeed()
	{
		if (!plr) {return 0.0;}
		return -0.15 * plr.countinv(GetClassName());
	}
}