//zscript_DI/items/passives/shellsling.zsc

Class DI_ShellSling : DIPassiveInventory
{
	Default
	{
		Tag "Shell Sling";
		Inventory.Icon "POW2M0";
		Inventory.PickupMessage "Shell Sling acquired! (Reloading is 25% faster if you have at least 75% ammo remaining.)";
		Inventory.MaxAmount 5;
	}
	
	override int ItemPriority() {return 1;}
	
	override double GiveReloadSpeed()
	{
		if (!plr || !wpn) {return 0.0;}
		bool givespeed = true;
		if (!wpn.Ammo1 && !wpn.Ammo2) {givespeed = false;}
		if (wpn.wpn_dontgiveammo1 && wpn.wpn_dontgiveammo2) {givespeed = false;}
		if (wpn.Ammo1)
		{
			if ((plr.CountInv(wpn.Ammo1.GetClassName()) < 0.75 * wpn.Ammo1.MaxAmount) && (!wpn.wpn_dontgiveammo1))
			{
				givespeed = false;
			}
		}
		if (wpn.Ammo2)
		{
			if ((plr.CountInv(wpn.Ammo2.GetClassName()) < 0.75 * wpn.Ammo2.MaxAmount) && (!wpn.wpn_dontgiveammo2))
			{
				givespeed = false;
			}
		}
		if (givespeed) {return -0.25 * plr.CountInv(self.GetClassName());}
		else {return  0.0;}
	}
	
	States
	{
		Spawn:
			POW1 B -1;
			Stop;
	}
}