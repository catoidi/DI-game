//zscript_DI/items/passives/laststand.zsc

Class DI_LastStand : DIPassiveInventory
{
	Default
	{
		Tag "Last Stand";
		Inventory.Icon "POW1G0";
		Inventory.PickupMessage "Acquired Last Stand! (Gain passive Regen upon reaching low enough HP.)";
		Inventory.MaxAmount 4;
	}
	
	int lasttimer;

	override void DoEffect(void)
	{
		Super.DoEffect();
		
		++lasttimer;
		
		if (owner && (lasttimer >= (70 - (owner.CountInv("DI_LastStand") * 5))))
		{
			if (owner is "DI_PlayerBase")
			{
				DI_PlayerBase plr = DI_PlayerBase(owner);
				
				float regennum = 0.1 + (0.15 * plr.CountInv("DI_LastStand"));
				if (plr.Health < ((plr.GetMaxHealth(true) - plr.CountInv("DI_Degen")) * regennum))
				{
					plr.A_GiveInventory("DI_Regen", 1);
				}
			}
			lasttimer = 0;
		}
	}
	
	States
	{
		Spawn:
			POW1 G -1;
			Stop;
	}
}