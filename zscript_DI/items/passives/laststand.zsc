//zscript_DI/items/passives/laststand.zsc

Class DI_LastStand : DIPassiveInventory
{
	Default
	{
		Tag "Last Stand";
		Inventory.Icon "POW1G0";
		Inventory.PickupMessage "Acquired Last Stand! (Gain passive Regen upon reaching low enough HP.)";
		Inventory.MaxAmount 4;
	}
	
	int lasttimer;
	bool savinggrace;

	override void DoEffect(void)
	{
		Super.DoEffect();
		
		++lasttimer;
		
		if (plr && (lasttimer >= (75 - (amount * 5))))
		{
			if (savinggrace == false) {savinggrace = true;}
			else if (plr.inwave)
			{
				float regennum = 0.1 + (0.15 * amount);
				if (plr.Health < (plr.DIMaxHealth() * regennum))
				{
					plr.DI_HealSelf(10, regennum, true);
				}
			}
			lasttimer = 0;
		}
	}
	
	override void ModifyDamage(int damage, Name damageType, out int newdamage, bool passive, Actor inflictor, Actor source, int flags)
	{
		if ((newdamage >= 1) && (owner) && (passive == true))
		{
			if ((newdamage >= owner.health) && (owner.health > 1) && (savinggrace == true))
			{
				newdamage = owner.health - 1;
				savinggrace = false;
			}
		}
	
		Super.ModifyDamage(damage, damageType, newdamage, passive, inflictor, source, flags);
	}
	
	States
	{
		Spawn:
			POW1 G -1;
			Stop;
	}
}