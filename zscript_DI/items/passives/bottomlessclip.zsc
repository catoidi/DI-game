//zscript_DI/items/passives/bottomlessclip.zsc

Class DI_BottomlessClip : DIPassiveInventory
{
	Default
	{
		Tag "Bottomless Clip";
		Inventory.Icon "POW2N0";
		Inventory.PickupMessage "Bottomless Clip acquired! (-2 Base DMG, +15% chance to not consume ammo.)";
		Inventory.MaxAmount 3;
	}
	
	bool clipactive;
	
	override void DoEffect()
	{
		Super.DoEffect();
	
		if (plr)
		{
			if (random(1, 20) <= (3 * amount)) {clipactive = true;}
			else {clipactive = false;}
		}
		else {clipactive = false;}
	}
	
	override int ItemPriority() {return 1;} //load this item after most other items
	
	override int GiveBaseDamage()
	{
		if (!plr) {return 0;}
		else {return -20 * amount;}
	}
	
	override int GiveAmmoUse1()
	{
		if (!clipactive) {return 0;}
		if (!wpn) {return 0;}
		if (wpn.wpn_dontgiveammo1) {return 0;}
		if (!wpn.Ammo1) {return 0;}
		if (plr.CountInv(wpn.Ammo1.GetClassName()) == 0) {return 0;}
		return -wpn.AmmoUse1;
	}
	
	override int GiveAmmoUse2()
	{
		if (!clipactive) {return 0;}
		if (!wpn) {return 0;}
		if (wpn.wpn_dontgiveammo2) {return 0;}
		if (!wpn.Ammo2) {return 0;}
		if (plr.CountInv(wpn.Ammo2.GetClassName()) == 0) {return 0;}
		return -wpn.AmmoUse2;
	}
	
	States
	{
		Spawn:
			POW2 N -1;
			Stop;
	}
}