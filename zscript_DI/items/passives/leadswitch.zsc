Class DI_LeadSwitch : DIPassiveInventory
{	
	DI_Weapon oldwpn;
	int ammogive;
	
	Default
	{
		Tag "Lead Switch";
		Inventory.Icon "POW1I0";
		Inventory.PickupMessage "Lead Switch acquired! (Firing has a 20% chance to give your next weapon +1 ammo.)";
		Inventory.MaxAmount 3;
	}
	
	override void DoEffect(void)
	{
		Super.DoEffect();
		
		if (plr && wpn && !plr.IsFrozen())
		{
			if ((wpn != oldwpn))
			{
				//console.printf("giving %d ammo to %s", ammogive, wpn.getclassname());
				DIGiveAmmo();
				oldwpn = wpn;
			}
			else if (plr.justfired)
			{
				if (random(1, 5) <= plr.countinv(self.getclassname()))
				{
					++ammogive;
					//console.printf("+1 ammo for next weapon");
				}
			}
			plr.setinventory("DI_LeadSwitch_Powerup", ammogive, true);
		}
	}
	
	virtual void DIGiveAmmo()
	{
		if (ammogive > 0)
		{
			let curammo1 = wpn.AmmoType1;
			let curammo2 = wpn.AmmoType2;
			if (curammo1 && (wpn.wpn_dontgiveammo1 == false))
			{
				plr.GiveInventory(curammo1, ammogive);
			}
			if (curammo2 && (wpn.wpn_dontgiveammo2 == false))
			{
				plr.GiveInventory(curammo2, ammogive);
			}
			ammogive = 0;
		}
	}
	
	States
	{
		Spawn:
			POW1 I -1;
			Stop;
	}
}


Class DI_LeadSwitch_Powerup : DIPowerup
{
	Default
	{
		Tag "\cvLead Switch (Powerup)";
		Inventory.Icon "POW1R0";
		Inventory.MaxAmount 999999;
	}
	
	override void PowerupTick() {} //remove timer code
}