Class DI_LeadSwitch : DIPassiveInventory
{	
	name oldwpn;
	name prevwpn;
	
	Default
	{
		Tag "Lead Switch";
		Inventory.Icon "POW1I0";
		Inventory.PickupMessage "Lead Switch acquired! (Switching to a fresh weapon will give you +1 ammo and faster reloading.)";
		Inventory.MaxAmount 3;
	}
	
	override void DoEffect(void)
	{
		Super.DoEffect();
		
		if (owner is "DI_PlayerBase")
		{
			let plr = DI_PlayerBase(owner);
			if ((plr.player.ReadyWeapon.GetClassName() != oldwpn) && (plr.FreshKillCount() > 0))
			{
				DIGiveAmmo(plr);
				oldwpn = plr.player.ReadyWeapon.GetClassName();
			}
		}
	}
	
	virtual void DIGiveAmmo(DI_PlayerBase plr)
	{
		let ammogivenum = plr.CountInv("DI_LeadSwitch");
		if (ammogivenum > 0)
		{
			let curwpn = DI_Weapon(plr.player.ReadyWeapon);
			let curammo1 = curwpn.AmmoType1;
			let curammo2 = curwpn.AmmoType2;
			if ((curwpn.GetClassName() != plr.LastWeaponKill) && (curwpn.GetClassName() != prevwpn))
			{
				plr.GiveInventory("DI_LeadSwitch_Powerup", ammogivenum * 5);
				if (curammo1 && (curwpn.wpn_dontgiveammo1 == false))
				{
					plr.GiveInventory(curammo1, ammogivenum);
					prevwpn = curwpn.GetClassName();
				}
				if (curammo2 && (curwpn.wpn_dontgiveammo2 == false))
				{
					plr.GiveInventory(curammo2, ammogivenum);
					prevwpn = curwpn.GetClassName();
				}
			}
		}
	}
	
	States
	{
		Spawn:
			POW1 I -1;
			Stop;
	}
}

Class DI_LeadSwitch_Powerup : DIPowerup
{
	Default
	{
		Tag "\cvLead Switch (Powerup)";
		Inventory.Icon "POW1R0";
		Inventory.MaxAmount 30;
	}
	
	override double GiveReloadSpeed() {return -0.5;}
}