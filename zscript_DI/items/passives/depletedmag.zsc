//zscript_DI/items/passives/depletedmag.zsc

Class DI_DepletedMag : DIPassiveInventory
{
	//increases firerate the closer the magazine is to empty.
	//gives up to 1% firerate per ammo used, maximum of 20%, based on ammo type's max amount.
	
	Default
	{
		Tag "Depleted Magazine";
		Inventory.Icon "POW1W0";
		Inventory.PickupMessage "Depleted Magazine acquired! (Increases firerate the closer the magazine is to empty.)";
		Inventory.MaxAmount 5;
	}
	
	override double GiveFirerate() 
	{
		if (!plr || !wpn) {return 0.0;}
		if (!wpn.ammotype1 && !wpn.ammotype2) {return 0.0;}
		int ammocount = 0;
		if (wpn.ammotype1)
		{
			int maxammo1 = wpn.GetMaxAmmo(); //expensive function!
			float divider = 1.0;
			if (maxammo1 > 20) {divider = 0.05 * maxammo1;}
			ammocount = (maxammo1 - plr.countinv(wpn.ammotype1.getclassname())) / divider;
			if (ammocount > 20) {ammocount = 20;}
		}
		if (wpn.ammotype2)
		{
			int maxammo2 = wpn.GetMaxAmmo(true); //expensive function!
			float divider = 1.0;
			if (maxammo2 > 20) {divider = 0.05 * maxammo2;}
			int ammocount2 = (maxammo2 - plr.countinv(wpn.ammotype2.getclassname())) / divider;
			if (ammocount2 > 20) {ammocount2 = 20;}
			if (ammocount2 < ammocount) {ammocount = ammocount2;} //prioritize whichever ammo type has the least amount consumed, to prevent cheesing.
		}
		
		return -0.01 * ammocount * plr.countinv(self.getclassname());
	}
	
	States
	{
		Spawn:
			POW1 W -1;
			Stop;
	}
}