//zscript_DI/items/passives/starfruit.zsc

Class DI_Starfruit : DIPassiveInventory
{
	Default
	{
		Tag "Starfruit";
		Inventory.Icon "POWRF0";
		Inventory.PickupMessage "Starfruit Acquired! (Combo-based chance to gain +20% DMG.)";
		Inventory.MaxAmount 10;
	}
	
	bool active;
	
	override void DoEffect()
	{
		Super.DoEffect();
	
		if (plr && !Level.IsFrozen())
		{
			if (random(plr.combonum, 9) == 9) {active = true;}
			else {active = false;}
		}
	}
	
	override string, int GiveProjectileItem()
	{
		if (!plr) {return "", 0;}
		if (!active) {return "", 0;}
		return "DI_Starfruit_Projectile", amount;
	}
	
	override double GiveDamageMult()
	{
		if (!plr) {return 1.0;}
		if (!active) {return 1.0;}
		return 1.0 + (0.2 * amount);
	}
	
	States
	{
		Spawn:
			POWR F -1;
			Stop;
	}
}

Class DI_Starfruit_Projectile : DIProjectileInventory
{
	override void ImpactActor(DI_PlayerBase plr, bool bounced, Actor victim)
	{
		owner.A_SpawnParticleEX("White", TexMan.CheckForTexture("POWRF0"), STYLE_None, SPF_FULLBRIGHT, 35, 16, 180, 0, 0, 0, 0, 0, 0.3, 0, 0, 0, 1, -1, 1);
	}
}