//zscript_DI/items/passives/fakestatups.zsc

//the code for these items isn't here, but within the player actor itself.
//for compatibility purposes, this is the WRONG way to create new items.

Class DI_BaseDamageGiver : DIPassiveInventory
{
	Default
	{
		Tag "Base DMG +";
		Inventory.Icon "POWRA0";
		Inventory.PickupMessage "Base Damage Increased! (+1 DMG)";
		Inventory.MaxAmount 25;
	}
	
	override int GiveBaseDamage()
	{
		if (!plr) {return 0;}
		return plr.countinv(GetClassName());
	}
	
	States
	{
		Spawn:
			POWR A -1;
			Stop;
	}
}

Class DI_ComboDamage : DIPassiveInventory
{
	Default
	{
		Tag "Combo DMG +";
		Inventory.Icon "POWRF0";
		Inventory.PickupMessage "Combo Damage Acquired! (+1 DMG per Combo)";
		Inventory.MaxAmount 3;
	}
	
	override int GiveBaseDamage()
	{
		if (!plr) {return 0;}
		return (plr.countinv(GetClassName()) * (plr.combonum - 1));
	}
	
	States
	{
		Spawn:
			POWR F -1;
			Stop;
	}
}

Class DI_ExtraCoinGiver : DIPassiveInventory
{
	Default
	{
		Tag "Inflation";
		Inventory.Icon "POWRB0";
		Inventory.PickupMessage "Inflation Rate Increased! (+1 Coin from all enemies)";
		Inventory.MaxAmount 5;
	}
	
	override int GiveExtraCoin()
	{
		if (!plr) {return 0;}
		return plr.CountInv(GetClassName());
	}
	
	States
	{
		Spawn:
			POWR B -1;
			Stop;
	}
}

Class DI_FreshBonusGiver : DIPassiveInventory
{
	Default
	{
		Tag "Fresh Bonus +";
		Inventory.Icon "POWRC0";
		Inventory.PickupMessage "Freshness Increased! (x2 to Fresh Weapon Bonuses)";
		Inventory.MaxAmount 5;
	}
	
	override int GiveFreshBonus()
	{
		if (!wpn || !plr) {return 0;}
		return (plr.CountInv(GetClassName()) * wpn.default.wpn_freshbonus);
	}
	
	States
	{
		Spawn:
			POWR C -1;
			Stop;
	}
}

Class DI_FreshBonusCharger : DIPassiveInventory
{
	Default
	{
		Tag "Fast Fresh";
		Inventory.Icon "POWRL0";
		Inventory.PickupMessage "Fast Fresh Acquired! (Shortens Fresh Weapon Cooldown)";
		Inventory.MaxAmount 5;
	}
	
	override int GiveFreshCharge()
	{
		if (!plr) {return 0;}
		return plr.CountInv(GetClassName());
	}
	
	States
	{
		Spawn:
			POWR L -1;
			Stop;
	}
}

Class DI_BulletSplitter : DIPassiveInventory
{
	//gives +1 projectile, but reduces accuracy by 3.
	Default
	{
		Tag "Bullet Splitter";
		Inventory.Icon "POWRG0";
		Inventory.PickupMessage "Bullet Splitter Acquired! (+1 Projectile, -3 Accuracy)";
		Inventory.MaxAmount 3;
	}
	
	override int GiveProjectiles()
	{
		if (!plr) {return 0;}
		return plr.CountInv(GetClassName());
	}
	
	override double GiveAccuracy()
	{
		if (!plr) {return 0.0;}
		return (plr.CountInv(GetClassName()) * 3.0);
	}
	
	States
	{
		Spawn:
			POWR G -1;
			Stop;
	}
}

Class DI_TooManyBullets : DIPassiveInventory
{
	Default
	{
		Tag "TooManyBullets";
		Inventory.Icon "POWRH0";
		Inventory.PickupMessage "Too Many Bullets Acquired! (Projectiles x2, -5 DMG)";
		Inventory.MaxAmount 5;
	}
	
	override int GiveProjectiles()
	{
		if (!wpn || !plr) {return 0;}
		return (plr.CountInv(GetClassName()) * wpn.default.wpn_projectiles);
	}
	
	override int GiveBaseDamage()
	{
		if (!plr) {return 0;}
		return plr.CountInv(GetClassName()) * -5;
	}
	
	States
	{
		Spawn:
			POWR H -1;
			Stop;
	}
}

Class DI_AmmoGiver : DIPassiveInventory
{
	Default
	{
		Tag "Ammo Giver";
		Inventory.Icon "POWRI0";
		Inventory.PickupMessage "Ammo Increased! (+33% Ammo Capacity)";
		Inventory.MaxAmount 10;
	}
	
	override int GiveMaxAmmo1()
	{
		if (!wpn || !plr) {return 0;}
		if (!wpn.Ammo1) {return 0;}
		else return plr.CountInv(GetClassName()) + (plr.CountInv(GetClassName()) * (wpn.Ammo1.MaxAmount / 3));
	}
	
	override int GiveMaxAmmo2()
	{
		if (!wpn || !plr) {return 0;}
		if (!wpn.Ammo2) {return 0;}
		else return plr.CountInv(GetClassName()) + (plr.CountInv(GetClassName()) * (wpn.Ammo2.MaxAmount / 3));
	}
	
	States
	{
		Spawn:
			POWR I -1;
			Stop;
	}
}

Class DI_PainStar : DIPassiveInventory
{
	Default
	{
		Tag "Pain Star";
		Inventory.Icon "POWRJ0";
		Inventory.PickupMessage "Pain Star Acquired! (+5 DMG, decreases with high combos.)";
		Inventory.MaxAmount 3;
	}
	
	override int GiveBaseDamage()
	{
		if (!plr) {return 0;}
		int painstarnum = plr.CountInv("DI_PainStar") * 6;
		painstarnum -= plr.CountInv("DI_PainStar") * plr.combonum;
		if (painstarnum < 0) {painstarnum = 0;}
		return painstarnum;
	}
	
	States
	{
		Spawn:
			POWR J -1;
			Stop;
	}
}

Class DI_Gunslinger : DIPassiveInventory
{
	Default
	{
		Tag "Gunslinger";
		Inventory.Icon "POWRK0";
		Inventory.PickupMessage "Gunslinger Acquired! (Gain +1 DMG for every weapon you own.)";
		Inventory.MaxAmount 3;
	}
	
	override int GiveBaseDamage()
	{
		if (!plr) {return 0;}
		return plr.weaponnum * plr.CountInv(GetClassName());
	}
	
	States
	{
		Spawn:
			POWR K -1;
			Stop;
	}
}