//zscript_DI/items/passives/bloodyheart.zsc

Class DI_BloodyHeart : DIPassiveInventory
{
	Default
	{
		Height 20;
		Radius 10;
		Tag "Bloody Heart";
		Inventory.Icon "POWRD0";
		Inventory.MaxAmount 3;
		Inventory.PickupMessage "Bloody Heart Acquired! (Drop HP Pickups upon taking damage. You take +1 DMG from all sources.)";
	}
	
	override void ModifyDamage(int damage, Name damageType, out int newdamage, bool passive, Actor inflictor, Actor source, int flags)
	{
		if ((damage >= 1) && (owner) && (passive == true))
		{
			for (int i = 0; i < amount; ++i) 
			{
				owner.A_SpawnItemEX("DI_BloodyHeart_Drop", 0, 0, 24, random(-6, 6), random(-6, 6), random(2, 6), 0, SXF_ABSOLUTEVELOCITY | SXF_NOCHECKPOSITION);
			
				newdamage += 10;
			}
		}
	
		Super.ModifyDamage(damage, damageType, newdamage, passive, inflictor, source, flags);
	}
	
	States
	{
		Spawn:
			POWR D -1;
			Stop;
	}
}

Class DI_BloodyHeart_Drop : DI_Actor
{
	Default
	{
		Height 20;
		Radius 10;
		RenderStyle "Translucent";
		Alpha 0.25;
	}

	States
	{
		Spawn:
			HEAL C 35;
			TNT1 A 10 A_SpawnItemEX("DI_BloodyHeart_Pickup", flags: SXF_NOCHECKPOSITION);
			Stop;
	}
}

Class DI_BloodyHeart_Pickup : Inventory
{
	Default
	{
		Height 20;
		Radius 10;
		Inventory.Icon "HEALC0";
		Inventory.PickupMessage "+2 Health";
		+Inventory.AUTOACTIVATE;
		+Inventory.ALWAYSPICKUP;
		+FLOATBOB;
		+BRIGHT;
	}
	
	override void PostBeginPlay()
	{
		Super.PostBeginPlay();
		
		ChangeTID(7499);
	}
	
	override void DoPickupSpecial(Actor toucher)
	{
		if (toucher is "DI_PlayerBase")
		{
			let plr = DI_PlayerBase(toucher);
			plr.DI_HealSelf(20);
		}
		
		eventhandler.sendnetworkevent("doheartpickup");
		Super.DoPickupSpecial(toucher);
	}
	
	States
	{
		Spawn:
			HEAL C 140;
			HEAL C 105 A_FadeOut(0.25);
			Wait;
	}
}