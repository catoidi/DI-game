//zscript_DI/items/objectives/sleightofhand.zsc

Class DI_QuestObjective_SleightOfHand : DI_QuestObjective
{
	//the player must ensure each kill is done using a different weapon for the entire wave to complete this.
	//getting multiple kills with the same weapon in a single frame will not fail the objective.
	weapon curweapon, prevweapon;
	bool queststarted;
	int coyotetime;
	int mistakes;
	
	override void doeffect()
	{
		super.doeffect();
		ObjectiveTick();
	}
	
	virtual void ObjectiveTick()
	{
		if (plr)
		{
			if (plr.inwave) {queststarted = true;}
			else if (queststarted) {ObjectiveComplete();}
			
			if (plr.killtic > 0)
			{
				curweapon = plr.player.readyweapon;
				if (curweapon == prevweapon) 
				{
					if (coyotetime > 35) 
					{
						++mistakes;
						plr.A_StartSound("piebomb", slot: CHAN_AUTO, volume: 0.5, pitch: 0.75 - (0.05 * mistakes) + (0.05 * plr.thebrain.wavenum));
						if (mistakes >= plr.thebrain.wavenum) {ObjectiveFailed();}
						coyotetime = 0;
					}
				}
				else 
				{
					prevweapon = curweapon;
					coyotetime = 0;
				}
			}
			
			if (queststarted)
			{
				++coyotetime;
			}
		}
	}

	override string QuestName() {return "Sleight Of Hand";}
	override string QuestProgress()
	{
		if (prevweapon) 
		{
			string returncolor = "\cf";
			if (coyotetime <= 35) {returncolor = "\cu";}
			return string.format("Prev. Weapon: %s%s\n\c-Mistakes: %d/%d", returncolor, prevweapon.gettag(), mistakes, plr.thebrain.wavenum);
		}
		else {return "N/A";}
	}
}