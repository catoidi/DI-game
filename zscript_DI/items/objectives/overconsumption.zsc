//zscript_DI/items/objectives/overconsumption.zsc

Class DI_QuestObjective_Overconsumption : DI_QuestObjective
{
	bool queststarted; //checks when the player first enters the next wave.
	bool chillout; //pause the timer while no monsters are alive.
	int timer;
	Property StartTimer : timer;
	Default {DI_QuestObjective_Overconsumption.StartTimer 1050;} //30 seconds.

	override void doeffect()
	{
		super.doeffect();
		ObjectiveTick();
	}
	
	virtual void ObjectiveTick()
	{
		if ((plr) && (plr.inwave))
		{
			queststarted = true;
			if (!chillout)
			{
				if (timer > 0) 
				{
					--timer;
				}
				else {ObjectiveFailed();}
			}
		}
		else if ((queststarted) && (!plr.inwave)) 
		{
			ObjectiveComplete();
		}
		
		if (Level.IsFrozen()) {chillout = true;}
		else if ((level.Total_Monsters > level.Killed_Monsters) && (plr.health > 0)) {chillout = false;}
		else {chillout = true;}
	}
	
	virtual void ItemUsed() {timer += default.timer;} //activated by DIHandler.

	override string QuestName() {return "Overconsumption";}
	override string QuestProgress()
	{
		double clock = timer / 35.0;
		return string.format("Time Left: %.2fs", clock);
	}
}