//zscript_DI/weapons/starter/cheesegun.zsc

Class CheeseGun : DI_Weapon
{
	Default
	{
		DI_Weapon.FreshBonus 4;
		DI_Weapon.BaseDamage 8;
		DI_Weapon.Projectiles 6;
		DI_Weapon.Recoil 10;
		DI_Weapon.RecoilRecover 0.5;
		DI_Weapon.RecoilOffset 1.5;
		DI_Weapon.BaseAccuracy 5;
		DI_Weapon.Ammo2HUD 3;
		Weapon.Kickback 0;
		Weapon.SelectionOrder 10000;
		Weapon.SlotNumber 1;
		Weapon.SlotPriority 1;
		Weapon.AmmoUse 1;
		Weapon.AmmoGive 1;
		Weapon.AmmoType "CheeseGunAmmo";
		Weapon.AmmoType2 "CheeseGunHeat";
		Inventory.Icon "CHESZ0";
		Tag "Cheese Cannon";
	}
	
	float cheeseglow;
	
	override void tick()
	{
		super.tick();
		
		if (!owner) {return;}
		if ((owner.CountInv("CheeseGunHeat") < Ammo2.MaxAmount) && (owner.CountInv("CheeseGunAmmo") > 0)) 
		{
			owner.A_GiveInventory("CheeseGunHeat", owner.CountInv("CheeseGunAmmo"));
			cheeseglow = owner.CountInv("CheeseGunHeat") / 100.0;
		}
	}

	States
	{
		Ready:
			CHES A 1 DI_WeaponReady(WRF_ALLOWRELOAD);
			Loop;
		Deselect:
			CHES # 0 A_Lower;
			Loop;
		Select:
			CHES A 0 
			{
				invoker.cheeseglow = CountInv("CheeseGunHeat") / 100.0;
				A_Overlay(2, "Glow");
			}
			Goto SelectLoop;
		SelectLoop:
			CHES AAA 0 A_Raise;
			CHES # 1 A_Raise;
			Loop;
		Fire:
			CHES # 0 A_JumpIfInventory("CheeseGunAmmo", 1, 2);
			CHES # 0 A_Jump(256, "NoReload");
			CHES B 2 BRIGHT 
			{
				A_Overlay(-2, "Flash");
				DI_FireProjectile("CheeseGunBullet", invoker.wpn_projectiles, -32768, invoker.AmmoUse1, -4, 4, FPF_AIMATANGLE);
				A_StartSound("pistpow");
			}
			CHES B 6 A_TakeInventory("CheeseGunHeat", 70);
			CHES A 0 A_Refire;
			CHES A 1;
			Goto Ready;
		Reload:
			CHES # 0 A_JumpIfInventory("CheeseGunReload", 1, "ReloadStart");
			CHES # 0 A_JumpIfInventory("CheeseGunAmmo", invoker.Ammo1.MaxAmount, "NoReload");
			CHES AA 1 A_OverlayOffset(PSP_WEAPON, 3.0, -0.5, WOF_ADD);
			CHES ###### 1 A_OverlayOffset(PSP_WEAPON, 9, -2, WOF_ADD);
			Goto ReloadStart+2;
		ReloadStart:
			CHES AA 1 A_OverlayOffset(PSP_WEAPON, 3.0, -0.5, WOF_ADD);
			//arrow reload start
			CHES # 22 {A_Overlay(-2, "Reload_Arrow");}
			CHES #### 1 A_OverlayOffset(PSP_WEAPON, -1.5, 0.25, WOF_ADD);
			//arrow reload end
			#### # 0 A_GiveInventory("CheeseGunAmmo", 1);
			#### # 0 A_GiveInventory("CheeseGunReload", 1);
			CHES A 2 A_JumpIfInventory("CheeseGunAmmo", invoker.Ammo1.MaxAmount, "ReloadEnd");
			CHES A 2 DI_WeaponReady(WRF_NOFIRE | WRF_NOSWITCH | WRF_ALLOWRELOAD);
			Goto ReloadEnd;
		Reload_Arrow:
			TNT1 A 0 A_OverlayOffset(-2, 0, 116, WOF_ADD);
			CHES CCCCCCCCCCCC 1 A_OverlayOffset(-2, 1, -8, WOF_ADD);
			CHES CCCC 1 A_OverlayOffset(-2, -10.5, 3, WOF_ADD);
			CHES C 4;
			CHES CCCCCC 1 A_OverlayOffset(-2, -3, 1.5, WOF_ADD);
			Stop;
		ReloadEnd:
			#### # 0 A_TakeInventory("CheeseGunReload");
			CHES AAAAAA 1 A_OverlayOffset(PSP_WEAPON, -9, 2, WOF_ADD);
			Goto Ready;
		NoReload:
			#### # 0 A_StartSound("noammo", starttime: 0.1);
			CHES # 1 A_OverlayOffset(PSP_WEAPON, 0, 4, WOF_ADD);
			CHES #### 1 A_OverlayOffset(PSP_WEAPON, 0, -1, WOF_ADD);
			CHES # 4;
			Goto Ready;
		Glow:
			CHES F 1 BRIGHT
			{
				if (health <= 0) {invoker.cheeseglow = 0.0;}
				A_OverlayFlags(2, PSPF_ALPHA | PSPF_FORCEALPHA, true);
				A_OverlayAlpha(2, invoker.cheeseglow);
			}
			Loop;
		Flash:
			CHES D 6 BRIGHT;
			Stop;
		Spawn:
			CHES Z -1;
			Stop;
	}
}

CLASS CheeseGunBullet : DI_Actor
{
	mixin DI_ProjectileMixin;

	Default
	{
		Mass 0;
		Gravity 0.25;
		Radius 4;
		Height 8;
		Speed 16;
		DamageFunction damnum;
		Projectile;
		-NOGRAVITY;
		+ROLLSPRITE;
		+BLOODSPLATTER;
	}
	
	int damnum;
	
	override void PostBeginPlay()
	{
		Super.PostBeginPlay();
		
		if ((target is "PlayerPawn") && (target.player.ReadyWeapon is "CheeseGun"))
		{
			CheeseGun tarwpn = CheeseGun(target.player.ReadyWeapon);
			damnum = tarwpn.wpn_damage;
			damnum *= tarwpn.damagemult;
			if (tarwpn.cheeseglow > 0.7) 
			{
				damagetype = "Fire"; 
				damnum *= (tarwpn.cheeseglow / 10) + 1.0;
			}
			pitch = target.pitch;
		}
		
		CheckStatusChange();
	}
	
	States
	{
		Spawn:
			TNT1 E 1 NoDelay A_SetRoll(random(0, 359));
			CHES E 35 BRIGHT;
			Wait;
		XDeath:
		Crash:
			CHES E 0 BRIGHT A_StartSound("weapons/laser/lasrhit");
		Death.Sky:
		Death:
			CHES E 1
			{
				A_FadeOut(0.02);
				A_DIScale(1.01, 1.01, true);
				if (invoker.Pos.Z <= FloorZ)
				{
					invoker.Scale.Y = invoker.Scale.X;
					invoker.Pitch = 0;
					+bFLATSPRITE = true;
				}
				else {invoker.Vel.Z += 0.01;}
			}
			Loop;
	}
}

Class CheeseGunAmmo : Ammo
{
	//it sure is ammo.
	Default
	{
		Radius 20;
		Height 24;
		Inventory.Amount 1;
		Inventory.MaxAmount 1;
		+CASTSPRITESHADOW;
	}
}

Class CheeseGunHeat : Ammo
{
	Default
	{
		Inventory.Amount 1;
		Inventory.MaxAmount 300;
	}
}

Class CheeseGunReload : GreenShotgunReload {}