//zscript_DI/weapons/starter/cheesegun.zsc

Class CheeseGun : DI_Weapon
{
	Default
	{
		DI_Weapon.FreshBonus 4;
		DI_Weapon.BaseDamage 16;
		DI_Weapon.Projectiles 2;
		DI_Weapon.Recoil 10;
		DI_Weapon.RecoilRecover 0.5;
		DI_Weapon.RecoilOffset 1.5;
		DI_Weapon.BaseAccuracy 5;
		DI_Weapon.Ammo2HUD 3;
		Weapon.Kickback 0;
		Weapon.SelectionOrder 10000;
		Weapon.SlotNumber 1;
		Weapon.SlotPriority 1;
		Weapon.AmmoUse 1;
		Weapon.AmmoUse2 70;
		Weapon.AmmoGive 1;
		Weapon.AmmoType "CheeseGunAmmo";
		Weapon.AmmoType2 "CheeseGunHeat";
		Inventory.Icon "CHESZ0";
		Inventory.Pickupmessage "Picked up the Cheese Cannon! (Generates heat over time. Does fire damage with enough heat.)";
		Tag "Cheese Cannon";
	}
	
	float cheeseglow;
	
	override void tick()
	{
		super.tick();
		
		if (!owner) {return;}
		if (owner.IsFrozen()) {return;}
		if ((owner.CountInv("CheeseGunHeat") < Ammo2.MaxAmount) && (owner.CountInv("CheeseGunAmmo") > 0)) 
		{
			owner.A_GiveInventory("CheeseGunHeat", owner.CountInv("CheeseGunAmmo"));
			cheeseglow = owner.CountInv("CheeseGunHeat") / 100.0;
		}
	}
	
	override void UpdateWeaponValues()
	{
		super.UpdateWeaponValues();
		wpn_projectiles += owner.CountInv("CheeseGunAmmo");
	}

	States
	{
		Ready:
			CHES A 1 DI_WeaponReady(WRF_ALLOWRELOAD);
			Loop;
		Deselect:
			CHES # 0 A_Lower;
			Loop;
		Select:
			CHES A 0 
			{
				invoker.cheeseglow = CountInv("CheeseGunHeat") / 100.0;
				A_Overlay(2, "Glow");
			}
			Goto SelectLoop;
		SelectLoop:
			CHES AAA 0 A_Raise;
			CHES # 1 A_Raise;
			Loop;
		Fire:
			CHES # 0 DI_FireCheck();
			CHES B 2 BRIGHT 
			{
				double pitchshift = 0.3;
				if (CountInv("CheeseGunHeat") < 300) {pitchshift = 0.001 * CountInv("CheeseGunHeat");}
				A_Overlay(-2, "Flash");
				DI_FireProjectile("CheeseGunBullet", invoker.wpn_projectiles, -32768, invoker.AmmoUse1, -4, 4, FPF_AIMATANGLE);
				DI_GunSound("Weapons/PartyShotty/SHOTFIRE", pitch: 1.2 - pitchshift);
			}
			CHES B 6 {invoker.DepleteAmmo(true);}
			CHES A 0 A_Refire;
			CHES A 1;
			Goto Ready;
		Reload:
			CHES # 0 DI_ReloadCheck();
			CHES # 0 A_JumpIfInventory("CheeseGunReload", 1, "ReloadStart");
			CHES AA 1 A_OverlayOffset(PSP_WEAPON, 3.0, -0.5, WOF_ADD);
			CHES ###### 1 A_OverlayOffset(PSP_WEAPON, 9, -2, WOF_ADD);
			Goto ReloadStart+2;
		ReloadStart:
			CHES AA 1 A_OverlayOffset(PSP_WEAPON, 3.0, -0.5, WOF_ADD);
			//arrow reload start
			CHES # 22 {A_Overlay(-2, "Reload_Arrow");}
			CHES #### 1 A_OverlayOffset(PSP_WEAPON, -1.5, 0.25, WOF_ADD);
			//arrow reload end
			#### # 0 A_GiveInventory("CheeseGunAmmo", 1);
			#### # 0 A_GiveInventory("CheeseGunReload", 1);
			CHES A 2 A_JumpIfInventory("CheeseGunAmmo", invoker.Ammo1.MaxAmount, "ReloadEnd");
			CHES A 2 DI_ContinueReload();
			Goto ReloadEnd;
		Reload_Arrow:
			TNT1 A 0 A_OverlayOffset(-2, 0, 116, WOF_ADD);
			CHES CCCCCCCCCCCC 1 A_OverlayOffset(-2, 1, -8, WOF_ADD);
			CHES CCCC 1 A_OverlayOffset(-2, -10.5, 3, WOF_ADD);
			CHES C 4;
			CHES CCCCCC 1 A_OverlayOffset(-2, -3, 1.5, WOF_ADD);
			Stop;
		ReloadEnd:
			#### # 0 A_TakeInventory("CheeseGunReload");
			CHES AAAAAA 1 A_OverlayOffset(PSP_WEAPON, -9, 2, WOF_ADD);
			Goto Ready;
		Glow:
			CHES F 1 BRIGHT
			{
				if (health <= 0) {invoker.cheeseglow = 0.0;}
				A_OverlayFlags(2, PSPF_ALPHA | PSPF_FORCEALPHA, true);
				A_OverlayAlpha(2, invoker.cheeseglow);
			}
			Loop;
		Flash:
			CHES D 6 BRIGHT;
			Stop;
		Spawn:
			CHES Z -1;
			Stop;
	}
}

CLASS CheeseGunBullet : DI_Projectile
{
	Default
	{
		Mass 0;
		Gravity 0.25;
		Radius 4;
		Height 8;
		Speed 16;
		DamageFunction damnum;
		Projectile;
		-NOGRAVITY;
		+ROLLSPRITE;
		+BLOODSPLATTER;
	}
	
	int damnum;
	
	override void PostBeginPlay()
	{
		Super.PostBeginPlay();
		
		if ((target is "PlayerPawn") && (target.player.ReadyWeapon is "CheeseGun"))
		{
			CheeseGun tarwpn = CheeseGun(target.player.ReadyWeapon);
			damnum = tarwpn.CheckDamage();
			if (tarwpn.cheeseglow > 0.7) 
			{
				damagetype = "Fire"; 
				damnum *= (tarwpn.cheeseglow / 10) + 1.0;
			}
			pitch = target.pitch;
		}
		else if (target is "DI_CheeseArtifact_Cloud")
		{
			damagetype = "Fire";
			damnum = 16;
			di_notweapon = true;
		}
	}
	
	override void Tick()
	{
		Super.Tick();
		if ((random(1, 10) == 1) && (damagetype == "Fire"))
		{
			A_SpawnParticleEX("White", TexMan.CheckForTexture("SMOKA0"), STYLE_NONE, SPF_ROLL, 70, 12, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0.25, 0.25, -1, 0, random(0, 359));
		}
	}
	
	States
	{
		Spawn:
			TNT1 E 1 NoDelay A_SetRoll(random(0, 359));
			CHES E 35 BRIGHT;
			Wait;
		XDeath:
		Crash:
			CHES E 0 BRIGHT A_StartSound("weapons/laser/lasrhit");
		Death.Sky:
		Death:
			CHES E 0
			{
				if (invoker.damagetype == "Fire")
				{
					A_SpawnParticleEX("White", TexMan.CheckForTexture("SMOKA0"), STYLE_NONE, SPF_ROLL, 140, 24, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0.125, 0.25, -1, 0, random(0, 359));
				}
				for (int i = 0; i < 5; ++i)
				{
					A_SpawnParticle("f8cc3b", 0, 35, 6, 0, 0, 0, 0, frandom(-2, 2), frandom(-2, 2), frandom(-2, 2), 0, 0, -0.33);
				}
			}
			CHES E 1
			{
				A_FadeOut(0.005);
				A_DIScale(1.0025, 1.0025, true);
				if (invoker.Pos.Z <= FloorZ)
				{
					invoker.Scale.Y = invoker.Scale.X;
					invoker.Pitch = 0;
					+bFLATSPRITE = true;
				}
				else {invoker.Vel.Z += 0.01;}
			}
			Wait;
	}
}

Class CheeseGunAmmo : Ammo
{
	//it sure is ammo.
	Default
	{
		Radius 20;
		Height 24;
		Inventory.Amount 1;
		Inventory.MaxAmount 1;
		+CASTSPRITESHADOW;
	}
}

Class CheeseGunHeat : Ammo
{
	Default
	{
		Inventory.Amount 1;
		Inventory.MaxAmount 300;
	}
}

Class CheeseGunReload : GreenShotgunReload {}