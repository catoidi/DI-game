//zscript_di/weapons/starpistol.zsc

CLASS StarPistol : DI_Weapon
{
	Default
	{
		DI_Weapon.FreshBonus 2;
		DI_Weapon.BaseDamage 90;
		DI_Weapon.Recoil 1.5;
		DI_Weapon.RecoilRecover 1.25;
		DI_Weapon.BaseAccuracy 3;
		Weapon.Kickback 250;
		Weapon.SelectionOrder 10000;
		Weapon.SlotNumber 1;
		Weapon.SlotPriority 1;
		Weapon.AmmoUse 1;
		Weapon.AmmoGive 8;
		Weapon.AmmoType "StarPistolAmmo";
		Inventory.Icon "PISTZ0";
		Inventory.Pickupmessage "Picked up the Star Pistol! (Damage increases with higher combos.)";
		Tag "Star Pistol";
		//$Category "DI/Weapons/"
		//$Title "Star Pistol"
		//$Color 9
	}

	override int CheckBaseDamage(float basescale)
	{
		//base damage is increased by 2 per combo mult.
		if (plr) 
		{
			int coindmg = plr.combonum + plr.combonum_bonus;
			if (plr.CanFreshKill()) {coindmg += wpn_freshbonus;}
			return (wpn_damage + (20 * coindmg)) * basescale;
		}
		else return Super.CheckBaseDamage(basescale);
	}
	
	States
	{
		Ready:
			PIST A 1 DI_WeaponReady(WRF_ALLOWRELOAD);
			Loop;
		Deselect:
			PIST A 0 A_Lower;
			Loop;
		Select:
			PIST AAA 0 A_Raise;
			PIST A 1 A_Raise;
			Loop;
		Fire:
			PIST B 0 DI_FireCheck();
			PIST G 2 BRIGHT 
			{
				A_Overlay(-2, "Flash");
				DI_Shoot();
				float powpitch = 0.01 * (105 - (CountInv("DI_TokenCombo") * 5));
				DI_GunSound("gunpow3", pitch: powpitch);
			}
			#### # 0 DI_BulletCasing(1);
			PIST GGFBBB 1;
			PIST B 0 A_Refire;
			PIST BB 1;
			Goto Ready;
		Reload:
			PIST A 0 DI_ReloadCheck();
			#### # 0 DI_StartSound("gunrel1", pitch: 1.2);
			PIST AAAA 1 A_OverlayOffset(PSP_WEAPON, 0, -12, WOF_ADD);
			PIST A 4;
			PIST A 0 A_Overlay(-2, "ReloadClip");
			PIST A 1 
			{
				A_OverlayOffset(PSP_WEAPON, 0, 4, WOF_ADD);
				A_SetPitch(invoker.owner.pitch + 0.5);
			}
			PIST AAAA 1 A_OverlayOffset(PSP_WEAPON, 0, -1, WOF_ADD);
			PIST A 13;
			PIST A 1 
			{
				A_OverlayOffset(PSP_WEAPON, 0, -4, WOF_ADD);
				A_SetPitch(invoker.owner.pitch - 0.5);
			}
			PIST AAAA 1 A_OverlayOffset(PSP_WEAPON, 0, 1, WOF_ADD);
			PIST A 0 A_GiveInventory("StarPistolAmmo", invoker.Ammo1.MaxAmount);
			PIST AAAA 1 A_OverlayOffset(PSP_WEAPON, 0, 12, WOF_ADD);
			Goto Ready;
		ReloadClip:
			PIST DDDDDDDDDDD 1 A_OverlayOffset(-2, 0.5, 12, WOF_ADD);
			#### # 0 DI_EjectMagazine("StarPistolMagazine");
			#### # 0 DI_StartSound("gunrel2");
			PIST EEEEEEE 1 A_OverlayOffset(-2, -0.75, -18, WOF_ADD);
			Stop;
		Flash:
			PIST C 2 BRIGHT DI_MuzzleFlash();
			#### # 0 DI_ClearFlash();
			Stop;
		Spawn:
			PIST Z -1;
			Stop;
	}
}

Class StarPistolAmmo : Ammo
{
	//it sure is ammo.
	Default
	{
		Radius 20;
		Height 24;
		Inventory.PickupMessage "$BOX_AMMO_STUPID";
		Inventory.Amount 8;
		Inventory.MaxAmount 8;
		Inventory.Icon "PISTE0";
		+CASTSPRITESHADOW;
	}
	
	States
	{
		Spawn:
			PIST F -1;
			Stop;
	}
}

Class StarPistolMagazine : DI_Casing1
{
	Default
	{
		Scale 0.16;
	}
	
	States
	{
		Spawn:
			PIST Y 0 NoDelay A_Jump(256, "TimeToRoll");
			Stop;
	}
}