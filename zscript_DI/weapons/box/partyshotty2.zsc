//zscript_di/weapons/box/partyshotty2.zsc

CLASS PartyShotty2 : DI_Weapon //20009
{
	Default
	{
		Radius 20;
		Height 24;
		DI_Weapon.FreshBonus 1;
		DI_Weapon.BaseDamage 15;
		DI_Weapon.Projectiles 12;
		DI_Weapon.BaseAccuracy 9;
		DI_Weapon.PlayerSpeed -0.1;
		DI_Weapon.Recoil 2;
		DI_Weapon.RecoilRecover 2;
		DI_Weapon.AutoOffset false;
		DI_Weapon.Ammo1HUD 4;
		Weapon.SlotNumber 2;
		Weapon.SlotPriority 1;
		Weapon.SelectionOrder 1000;
		Weapon.Kickback 200;
		Weapon.AmmoUse 4;
		Weapon.AmmoGive 24;
		Weapon.AmmoType "BoxAmmo";
		DamageType "PartyShot";
		Inventory.PickupMessage "Acquired the Super Party Shotgun! (+24 Party Ammo)";
		Inventory.Icon "SHU1Z0";
		//Inventory.AltHUDIcon "AMMOB0";
		Obituary "$BOX_DIE_SHUT";
		Tag "Super Party Shotgun";
		+WEAPON.ALT_USES_BOTH;
		-WEAPON.AMMO_OPTIONAL;
		+CASTSPRITESHADOW;
		+COUNTITEM;
		//$Category "Box Party/Weapons"
		//$Title "PartyShotty (Obligatory Shotgun)"
		//$Color 9
	}
	
	action void A_BoxPartyShoot (void)
	{
		DI_FireProjectile("PartyShottyBullet", -32768, -32768, -32768, -4, 4, FPF_AIMATANGLE, -32768, isRecoil: false);
		A_FireProjectile("PartyShottySmoke", 0, 0, -4, 12, FPF_AIMATANGLE, 0);
		A_StartSound("Weapons/PartyShotty/SHOTFIRE", CHAN_WEAPON);
		//invoker.recnum += 1;
	}
	
	action void A_BoxPartyShootAlt (void)
	{
		//invoker.recnum += 3;
		invoker.DoRecoil();
		invoker.DoRecoil();
		DI_FireProjectile("PartyShottyBullet", -32768, -32768, -32768, -4, 4, FPF_AIMATANGLE, -32768, isRecoil: false);
		A_FireProjectile("PartyShottySmoke", 0, 0, -4, 12, FPF_AIMATANGLE, 0);
		A_StartSound("Weapons/PartyShotty/SHOTFIRE", CHAN_WEAPON);
	}
	
	override void Tick()
	{
		Super.Tick();
		if (plr)
		{
			plr.boxammonum += 1;
		}
	}
	
	States
	{
		Ready:
			SHU1 A 0 WeaponMode(0);
			SHU1 A 1 A_WeaponReady;
			Loop;
		Deselect:
			SHU1 A 0 A_Lower;
			Loop;
		Select:
			SHU1 AAA 0 A_Raise;
			SHU1 A 1 A_Raise;
			Loop;
		Fire:
			SHU1 # 0;
			SHU1 # 0 DI_FireCheck();
			SHU1 C 0 A_BoxPartyShoot();
			SHU1 C 1 A_WeaponOffset(-15, 35, WOF_INTERPOLATE);
			SHU1 C 1 A_WeaponOffset(-20, 40, WOF_INTERPOLATE);
			SHU1 C 1 A_WeaponOffset(-10, 30, WOF_INTERPOLATE);
			SHU1 B 2 A_WeaponOffset(0, 20, WOF_INTERPOLATE);
			SHU1 B 12;
			SHU1 BB 1 A_OverlayOffset(PSP_WEAPON, 0, 2, WOF_ADD);
			SHU1 F 0 A_Overlay(-2, "Barrel1");
			SHU1 F 0 A_Overlay(2, "Barrel2");
			SHU1 F 1 A_OverlayOffset(PSP_WEAPON, 0, 8, WOF_ADD);
			SHU1 FFFF 1 A_OverlayOffset(PSP_WEAPON, 0, -2, WOF_ADD);
			SHU1 F 31;
			SHU1 B 1 A_OverlayOffset(PSP_WEAPON, 0, -6, WOF_ADD);
			SHU1 BBBB 1 A_OverlayOffset(PSP_WEAPON, 0, 1.5, WOF_ADD);
			SHU1 A 0 A_Refire;
			SHU1 A 5 A_WeaponOffset(-1, 20, WOF_INTERPOLATE);
			//#### # 0 {invoker.recnum = 0;}
			Goto Ready;
		Barrel1:
			SHU1 G 1;
			SHU1 GGGGGGGGGGG 1 A_OverlayOffset(-2, 3, 12, WOF_ADD);
			TNT1 A 16;
			SHU1 GGGGGGGG 1 A_OverlayOffset(-2, -4.125, -16.5, WOF_ADD);
			Stop;
		Barrel2:
			SHU1 H 1;
			SHU1 HHHHHHHHHHH 1 A_OverlayOffset(2, 3, 12, WOF_ADD);
			TNT1 A 16;
			SHU1 HHHHHHHH 1 A_OverlayOffset(2, -4.125, -16.5, WOF_ADD);
			Stop;
		AltFire:
			SHU1 B 0;
			SHU1 B 0 DI_FireCheck();
			SHU1 B 0 A_BoxPartyShootAlt();
			SHU1 B 1 A_WeaponOffset(-15, 35, WOF_INTERPOLATE);
			SHU1 B 1 A_WeaponOffset(-20, 40, WOF_INTERPOLATE);
			SHU1 B 1 A_WeaponOffset(-10, 30, WOF_INTERPOLATE);
			SHU1 B 1 A_WeaponOffset(0, 20, WOF_INTERPOLATE);
			SHU1 A 1;
			SHU1 B 0 A_JumpIfInventory("BoxAmmo", 2, 2);
			SHU1 B 0 A_Jump(256, 2);
			SHU1 B 0 A_BoxPartyShootAlt();
			SHU1 B 1 A_WeaponOffset(-15, 35, WOF_INTERPOLATE);
			SHU1 B 1 A_WeaponOffset(-20, 40, WOF_INTERPOLATE);
			SHU1 B 1 A_WeaponOffset(-10, 30, WOF_INTERPOLATE);
			SHU1 B 1 A_WeaponOffset(0, 20, WOF_INTERPOLATE);
			SHU1 A 1;
			SHU1 B 0 A_JumpIfInventory("BoxAmmo", 2, 2);
			SHU1 B 0 A_Jump(256, 2);
			SHU1 B 0 A_BoxPartyShootAlt();
			SHU1 B 1 A_WeaponOffset(-15, 35, WOF_INTERPOLATE);
			SHU1 B 1 A_WeaponOffset(-20, 40, WOF_INTERPOLATE);
			SHU1 B 1 A_WeaponOffset(-10, 30, WOF_INTERPOLATE);
			SHU1 B 1 A_WeaponOffset(0, 20, WOF_INTERPOLATE);
			SHU1 A 16;
			SHU1 E 0 A_StartSound("Weapons/PartyShotty/SHOTLOAD", CHAN_WEAPON);
			SHU1 E 5 A_WeaponOffset(-5, 24, WOF_INTERPOLATE);
			SHU1 E 5 A_WeaponOffset(-13, 36, WOF_INTERPOLATE);
			SHU1 A 5 A_WeaponOffset(-9, 28, WOF_INTERPOLATE);
			//SHU1 A 0 {invoker.recnum = 0;}
			SHU1 A 0 A_Refire;
			SHU1 A 5 A_WeaponOffset(-1, 20, WOF_INTERPOLATE);
			Goto Ready;
		Spawn:
			SHU1 Z -1;
			Stop;
	}
}