//zboxscript/weapons/partyshotty.zsc

CLASS PartyShotty : DI_Weapon //20009
{
	Default
	{
		Radius 20;
		Height 24;
		DI_Weapon.FreshBonus 1;
		DI_Weapon.BaseDamage 13;
		DI_Weapon.Projectiles 6;
		DI_Weapon.BaseAccuracy 6;
		DI_Weapon.Recoil 1;
		DI_Weapon.RecoilRecover 2;
		DI_Weapon.AutoOffset false;
		DI_Weapon.Ammo1HUD 4;
		Weapon.SlotNumber 2;
		Weapon.SlotPriority 1;
		Weapon.SelectionOrder 1000;
		Weapon.Kickback 200;
		Weapon.AmmoUse 3;
		Weapon.AmmoGive 24;
		Weapon.AmmoType "BoxAmmo";
		DamageType "PartyShot";
		Inventory.PickupMessage "Acquired the Party Shotgun. (+24 Party Ammo)";
		Inventory.Icon "SHUTZ0";
		//Inventory.AltHUDIcon "AMMOB0";
		Obituary "$BOX_DIE_SHUT";
		Tag "Party Shotgun";
		+WEAPON.ALT_USES_BOTH;
		-WEAPON.AMMO_OPTIONAL;
		+CASTSPRITESHADOW;
		+COUNTITEM;
		//$Category "Box Party/Weapons"
		//$Title "PartyShotty (Obligatory Shotgun)"
		//$Color 9
	}
	
	action void A_BoxPartyShoot (void)
	{
		DI_FireProjectile("PartyShottyBullet", -32768, -32768, -32768, -4, 4, FPF_AIMATANGLE, -32768, isRecoil: false);
		A_FireProjectile("PartyShottySmoke", 0, 0, -4, 12, FPF_AIMATANGLE, 0);
		A_StartSound("Weapons/PartyShotty/SHOTFIRE", CHAN_WEAPON);
		//invoker.recnum += 1;
	}
	
	action void A_BoxPartyShootAlt (void)
	{
		//invoker.recnum += 3;
		invoker.DoRecoil();
		invoker.DoRecoil();
		DI_FireProjectile("PartyShottyBullet", -32768, -32768, -32768, -4, 4, FPF_AIMATANGLE, -32768, isRecoil: false);
		A_FireProjectile("PartyShottySmoke", 0, 0, -4, 12, FPF_AIMATANGLE, 0);
		A_StartSound("Weapons/PartyShotty/SHOTFIRE", CHAN_WEAPON);
	}
	
	States
	{
		Ready:
			SHUT A 0 WeaponMode(0);
			SHUT A 1 A_WeaponReady;
			Loop;
		Deselect:
			SHUT A 0 A_Lower;
			Loop;
		Select:
			SHUT AAA 0 A_Raise;
			SHUT A 1 A_Raise;
			Loop;
		Fire:
			SHUT B 0;
			SHUT B 0 DI_FireCheck();
			SHUT B 0 A_BoxPartyShoot();
			SHUT B 1 A_WeaponOffset(-15, 35, WOF_INTERPOLATE);
			SHUT B 1 A_WeaponOffset(-20, 40, WOF_INTERPOLATE);
			SHUT B 1 A_WeaponOffset(-10, 30, WOF_INTERPOLATE);
			SHUT B 2 A_WeaponOffset(0, 20, WOF_INTERPOLATE);
			SHUT A 12;
			SHUT E 0 A_StartSound("Weapons/PartyShotty/SHOTLOAD", CHAN_WEAPON);
			SHUT E 5 A_WeaponOffset(-5, 24, WOF_INTERPOLATE);
			SHUT E 5 A_WeaponOffset(-13, 36, WOF_INTERPOLATE);
			SHUT A 5 A_WeaponOffset(-9, 28, WOF_INTERPOLATE);
			SHUT A 0 A_Refire;
			SHUT A 5 A_WeaponOffset(-1, 20, WOF_INTERPOLATE);
			//#### # 0 {invoker.recnum = 0;}
			Goto Ready;
		AltFire:
			SHUT B 0;
			SHUT B 0 DI_FireCheck();
			SHUT B 0 A_BoxPartyShootAlt();
			SHUT B 1 A_WeaponOffset(-15, 35, WOF_INTERPOLATE);
			SHUT B 1 A_WeaponOffset(-20, 40, WOF_INTERPOLATE);
			SHUT B 1 A_WeaponOffset(-10, 30, WOF_INTERPOLATE);
			SHUT B 1 A_WeaponOffset(0, 20, WOF_INTERPOLATE);
			SHUT A 1;
			SHUT B 0 A_JumpIfInventory("BoxAmmo", 2, 2);
			SHUT B 0 A_Jump(256, 2);
			SHUT B 0 A_BoxPartyShootAlt();
			SHUT B 1 A_WeaponOffset(-15, 35, WOF_INTERPOLATE);
			SHUT B 1 A_WeaponOffset(-20, 40, WOF_INTERPOLATE);
			SHUT B 1 A_WeaponOffset(-10, 30, WOF_INTERPOLATE);
			SHUT B 1 A_WeaponOffset(0, 20, WOF_INTERPOLATE);
			SHUT A 1;
			SHUT B 0 A_JumpIfInventory("BoxAmmo", 2, 2);
			SHUT B 0 A_Jump(256, 2);
			SHUT B 0 A_BoxPartyShootAlt();
			SHUT B 1 A_WeaponOffset(-15, 35, WOF_INTERPOLATE);
			SHUT B 1 A_WeaponOffset(-20, 40, WOF_INTERPOLATE);
			SHUT B 1 A_WeaponOffset(-10, 30, WOF_INTERPOLATE);
			SHUT B 1 A_WeaponOffset(0, 20, WOF_INTERPOLATE);
			SHUT A 16;
			SHUT E 0 A_StartSound("Weapons/PartyShotty/SHOTLOAD", CHAN_WEAPON);
			SHUT E 5 A_WeaponOffset(-5, 24, WOF_INTERPOLATE);
			SHUT E 5 A_WeaponOffset(-13, 36, WOF_INTERPOLATE);
			SHUT A 5 A_WeaponOffset(-9, 28, WOF_INTERPOLATE);
			//SHUT A 0 {invoker.recnum = 0;}
			SHUT A 0 A_Refire;
			SHUT A 5 A_WeaponOffset(-1, 20, WOF_INTERPOLATE);
			Goto Ready;
		Spawn:
			SHUT Z -1;
			Stop;
	}
}

CLASS PartyShottyBullet : DI_Actor
{	
	mixin DI_ProjectileMixin;

	Default
	{
		Mass 0;
		Gravity 0.25;
		Radius 4;
		Height 8;
		Speed 24;
		DamageFunction throwdam;
		DamageType "PartyShot";
		Projectile;
		-NOGRAVITY;
		+BLOODSPLATTER;
		//+INTERPOLATEANGLES;
		+ROLLSPRITE;
		+SPRITEANGLE;
		+FORCEXYBILLBOARD;
	}
	
	int throwdam;
	override void PostBeginPlay()
	{
		Super.PostBeginPlay();
		
		CheckStatusChange();
		
		throwdam = 15;
		if ((target is "PlayerPawn") && (target.player.ReadyWeapon is "PartyShotty"))
		{
			PartyShotty tarwpn = PartyShotty(target.player.ReadyWeapon);
			throwdam = tarwpn.wpn_damage;
			throwdam *= tarwpn.damagemult;
		}
	}
	
	action void FakeShottyCrumb()
	{
		A_SpawnParticleEX("White", TexMan.CheckForTexture("SHUTX0"), 0, SPF_ROLL, 24, 4.0, random(0, 359), random(-2, 2), random(-2, 2), random(-2, 2), random(-2, 2), random(-2, 2), random(-2, 3), 0, 0, -0.33, 1, 0, -0.16, random(0, 359), 3 - (6 * (random(0, 1))));
	}
	
	States
	{
		Spawn:
			TNT1 A 0 NoDelay A_Jump(256, "Firsty", "Secondy", "Thirdy");
			Stop;
		Firsty:
			SHUT U 1;
			goto SpeenStart;
		Secondy:
			SHUT V 1;
			goto SpeenStart;
		Thirdy:
			SHUT W 1;
			goto SpeenStart;
		SpeenStart:
			"####" "#" 1 A_SetRoll(roll + 8);
			Loop;
		Death.Sky:
		Death:
			TNT1 A 1 
			{
				A_ProjectileSmoke();
				FakeShottyCrumb();
				FakeShottyCrumb();
				FakeShottyCrumb();
				if (invoker.damagetype != invoker.default.damagetype) {DIProjectileTrail(3.0);}
			}
			Stop;
	}
}

CLASS PartyShottySmoke : DI_Actor
{
	//the puff that comes from your gun when you fire it.
	//ive tried my best to make the smoke as unobnoxious as possible.

	Default
	{
		Gravity 0;
		Speed 5;
		Radius 2;
		Height 4;
		Mass 0;
	
		-SOLID;
		-BLOCKEDBYSOLIDACTORS;
		+MISSILE;
		+INTERPOLATEANGLES;
		+ROLLSPRITE;
		+SPRITEANGLE;
		+FORCEXYBILLBOARD;
	}
	
	States
	{
		Spawn:
			TNT1 A 0 NoDelay A_SetRoll(random(0, 359));
			SMOK A 0 A_FadeOut(0.95);
			SMOK A 1 A_Jump(128, "SpeenLeft");
		SpeenRight:
			"####" "#" 1 A_SetRoll(roll - 3);
			"####" "#" 0 A_FadeOut(0.01);
			Loop;
		SpeenLeft:
			"####" "#" 1 A_SetRoll(roll + 3);
			"####" "#" 0 A_FadeOut(0.01);
			Loop;
		Death:
			"####" "#" 0 A_FadeOut(0.01);
			Stop;
	}
}