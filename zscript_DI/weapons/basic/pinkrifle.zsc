//pinkrifle.zsc

CLASS PinkRifle : DI_Weapon
{
	Default
	{
		DI_Weapon.FreshBonus 3;
		DI_Weapon.BaseDamage 200;
		DI_Weapon.PlayerSpeed -0.1;
		DI_Weapon.Recoil 5.0;
		DI_Weapon.RecoilRecover 1.25;
		DI_Weapon.RecoilOffset 2.5;
		Weapon.Kickback 100;
		Weapon.SelectionOrder 10000;
		Weapon.SlotNumber 3;
		Weapon.SlotPriority 1;
		Weapon.AmmoUse 1;
		Weapon.AmmoGive 20;
		Weapon.AmmoType "PinkRifleAmmo";
		Inventory.PickupMessage "Acquired the Pink Rifle.";
		Inventory.Icon "PRIFZ0";
		//Obituary "$BOX_DIE_STUPID";
		//Inventory.Pickupmessage "$BOX_WEAPON_STUPID";
		Tag "Pink Rifle";
		//$Category "DI/Weapons/"
		//$Title "Star Pistol"
		//$Color 9
	}
	
	action void PinkRifleShoot(int flashnum)
	{
		if (flashnum == 0) {A_Overlay(-2, "Flash");}
		else {A_Overlay(-2, "Flash2");}
		DI_Shoot();
		int recamount = 10;
		DI_GunSound("gunpow2", pitch: 0.9);
	}
	
	action state PinkRifleStop(bool recover)
	{
		if (recover == true)
		{
			if (invoker.current_recoil < 5.0) {return ResolveState("Ready");}
		}
		return ResolveState(null);
	}
	
	States
	{
		Ready:
			PRIF A 1 DI_WeaponReady(WRF_ALLOWRELOAD);
			Loop;
		Deselect:
			PRIF A 0 A_Lower;
			Loop;
		Select:
			PRIF AAA 0 A_Raise;
			PRIF A 1 A_Raise;
			Loop;
		Fire:
			PRIF B 0 DI_FireCheck();
			PRIF B 2 BRIGHT PinkRifleShoot(0);
			#### # 0 DI_BulletCasing(2);
			PRIF C 5;
			PRIF C 0 A_Refire;
			PRIF CCCCC 1 PinkRifleStop(false);
			PRIF C 1 PinkRifleStop(true);
			Wait;
		Reload:
			PRIF A 0 DI_ReloadCheck();
			PRIF A 0 A_OverlayPivot(PSP_WEAPON, 0.2, 0.3);
			#### # 0 DI_StartSound("hpnmov1");
			PRIF AAAAAAAAAAAA 1 
			{
				A_OverlayRotate(PSP_WEAPON, 4, WOF_ADD);
				A_SetPitch(invoker.owner.pitch - 0.25);
			}
			PRIF A 6;
			#### # 0 DI_StartSound("gunmov1", pitch: 1.0);
			PRIF A 0 A_Overlay(-2, "ReloadClip");
			PRIF AA 1 A_OverlayOffset(PSP_WEAPON, 0, 2, WOF_ADD);
			PRIF G 1 
			{
				A_OverlayOffset(PSP_WEAPON, 0, 8, WOF_ADD);
				A_SetPitch(invoker.owner.pitch + 1.5);
			}
			PRIF GGGG 1 A_OverlayOffset(PSP_WEAPON, 0, -2, WOF_ADD);
			PRIF G 31;
			PRIF A 1 
			{
				A_OverlayOffset(PSP_WEAPON, 0, -6, WOF_ADD);
				A_SetPitch(invoker.owner.pitch - 1.125);
			}
			PRIF AAAA 1 A_OverlayOffset(PSP_WEAPON, 0, 0.5, WOF_ADD);
			PRIF A 0 A_GiveInventory("PinkRifleAmmo", invoker.Ammo1.MaxAmount);
			#### # 0 DI_StartSound("hpnmov1", pitch: 1.2, startTime: 0.4);
			PRIF AAAAAAAAAAAA 1 
			{
				A_OverlayRotate(PSP_WEAPON, -4, WOF_ADD);
				A_SetPitch(invoker.owner.pitch + 0.25);
			}
			Goto Ready;
		ReloadClip:
			TNT1 A 1 {A_OverlayPivot(-2, 0.2, 0.3); A_OverlayRotate(-2, 48, WOF_ADD);}
			TNT1 A 1 A_OverlayOffset(-2, 0, -64, WOF_ADD);
			PRIF E 1;
			PRIF EEEEEEEEEEE 1 A_OverlayOffset(-2, 6, 12, WOF_ADD);
			#### # 0 DI_EjectMagazine("PinkRifleMagazine");
			TNT1 A 13;
			#### # 0 DI_StartSound("gunrel2", pitch: 0.8);
			TNT1 A 3;
			PRIF FFFFFFFF 1 A_OverlayOffset(-2, -8.25, -16.5, WOF_ADD);
			Stop;
		Flash:
			PRIF D 2 BRIGHT DI_MuzzleFlash();
			#### # 0 DI_ClearFlash();
			Stop;
		Flash2:
			PRIF H 2 BRIGHT DI_MuzzleFlash();
			#### # 0 DI_ClearFlash();
			Stop;
		Spawn:
			PRIF Z -1;
			Stop;
	}
}

Class PinkRifleAmmo : Ammo
{
	//it sure is ammo.
	Default
	{
		Radius 20;
		Height 24;
		Inventory.PickupMessage "$BOX_AMMO_STUPID";
		Inventory.Amount 16;
		Inventory.MaxAmount 16;
		Inventory.Icon "PRIFF0";
		+CASTSPRITESHADOW;
	}
	
	States
	{
		Spawn:
			PRIF F -1;
			Stop;
	}
}

Class PinkRifleMagazine : DI_Casing1
{
	Default
	{
		Scale 0.16;
	}
	
	States
	{
		Spawn:
			PRIF Y 0 NoDelay A_Jump(256, "TimeToRoll");
			Stop;
	}
}