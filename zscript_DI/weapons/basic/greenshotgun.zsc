//zscript_di/weapons/starpistol.zsc

CLASS GreenShotgun : DI_Weapon
{
	Default
	{
		DI_Weapon.FreshBonus 2;
		DI_Weapon.BaseDamage 8;
		DI_Weapon.BaseAccuracy 5;
		DI_Weapon.Projectiles 8;
		DI_Weapon.Recoil 4.0;
		DI_Weapon.RecoilOffset 4.0;
		DI_Weapon.RecoilRecover 2.0;
		DI_Weapon.Ammo2Hud 1;
		DI_Weapon.DontGiveAmmo2 true;
		Weapon.Kickback 50;
		Weapon.SelectionOrder 10000;
		Weapon.SlotNumber 2;
		Weapon.SlotPriority 1;
		Weapon.AmmoUse 1;
		Weapon.AmmoGive 6;
		Weapon.AmmoGive2 1;
		Weapon.AmmoType "GreenShotgunAmmo";
		Weapon.AmmoType2 "GreenShotgunPump";
		Inventory.PickupMessage "Acquired the Green Shotgun.";
		Inventory.Icon "SHOTZ0";
		//Obituary "$BOX_DIE_STUPID";
		//Inventory.Pickupmessage "$BOX_WEAPON_STUPID";
		Tag "Green Shotgun";
		//$Category "DI/Weapons/"
		//$Title "Green Shotgun"
		//$Color 9
	}
	
	States
	{
		Ready:
			SHOT A 1 DI_WeaponReady(WRF_ALLOWRELOAD);
			Loop;
		Deselect:
			SHOT A 0 A_Lower;
			Loop;
		Select:
			SHOT A 0 A_TakeInventory("GreenShotgunReload");
			SHOT AAA 0 A_Raise;
			SHOT A 1 A_Raise;
			Loop;
		Fire:
			SHOT C 0;
			SHOT C 0 A_JumpIfInventory("GreenShotgunPump", 1, 2);
			SHOT C 0 A_Jump(256, "NoReload");
			SHOT C 2 BRIGHT 
			{
				A_Overlay(-2, "Flash");
				A_Overlay(2, "Fire_Barrel");
				A_SetPitch(pitch - 1.6);
				DI_BasicBullet(invoker.wpn_damage, false);
				A_StartSound("pistpow");
				A_TakeInventory("GreenShotgunPump", 1);
				//A_OverlayOffset(PSP_WEAPON, -12, 12, WOF_ADD);
			}
			SHOT BBBBBBBB 1; //A_OverlayOffset(PSP_WEAPON, 1.5, -1.5, WOF_ADD);
			SHOT B 0 DI_WeaponReady(WRF_NOPRIMARY);
			Goto Ready;
		Fire_Barrel:
			SHOT F 2 BRIGHT;
			SHOT F 12;
			Stop;
		AltFire:
			#### # 0 A_JumpIfInventory("GreenShotgunPump", 1, "NoReload");
			#### # 0 A_JumpIfInventory("GreenShotgunAmmo", 1, 2);
			#### # 0 A_Jump(256, "NoReload");
			#### # 0 A_StartSound("Weapons/PartyShotty/SHOTLOAD", CHAN_WEAPON);
			SHOT B 0 A_Overlay(2, "Pump_Barrel");
			SHOT B 1 A_OverlayOffset(PSP_WEAPON, 0, -3, WOF_ADD);
			SHOT BBBBBB 1 A_OverlayOffset(PSP_WEAPON, -0.5, 0.5, WOF_ADD);
			#### # 0 DI_BulletCasing(0);
			SHOT D 3;
			SHOT DDDDDD 1 A_OverlayOffset(PSP_WEAPON, 0.5, -0.5, WOF_ADD);
			SHOT B 1 A_OverlayOffset(PSP_WEAPON, 0, 3, WOF_ADD);
			#### # 0 A_TakeInventory("GreenShotgunAmmo", 1);
			SHOT B 3 A_GiveInventory("GreenShotgunPump", 1);
			Goto Ready;
		Pump_Barrel:
			SHOT F 1;
			SHOT FFFFFF 1 A_OverlayOffset(2, -2, 0.67, WOF_ADD);
			SHOT F 3;
			SHOT FFFFFF 1 A_OverlayOffset(2, 2, -0.67, WOF_ADD);
			SHOT F 4;
			Stop;
		Reload:
			SHOT A 0 A_JumpIfInventory("GreenShotgunReload", 1, "ReloadStart");
			SHOT A 0 A_JumpIfInventory("GreenShotgunAmmo", invoker.Ammo1.MaxAmount, "NoReload");
			SHOT AAAAAA 1 A_OverlayOffset(PSP_WEAPON, 10, -2, WOF_ADD);
			Goto ReloadStart;
		ReloadStart:
			SHOT D 10 {A_Overlay(2, "Reload_Barrel"); A_Overlay(3, "Reload_Shell");}
			SHOT D 4 A_OverlayOffset(PSP_WEAPON, 0, 2, WOF_ADD);
			SHOT B 2 A_OverlayOffset(PSP_WEAPON, 0, -2, WOF_ADD);
			#### # 0 A_GiveInventory("GreenShotgunAmmo", 1);
			SHOT A 0 A_GiveInventory("GreenShotgunReload", 1);
			SHOT A 2 A_JumpIfInventory("GreenShotgunAmmo", invoker.Ammo1.MaxAmount, "ReloadEnd");
			SHOT A 2 DI_WeaponReady(WRF_NOFIRE | WRF_NOSWITCH | WRF_ALLOWRELOAD);
			Goto ReloadEnd;
		Reload_Barrel:
			SHOT F 16;
			Stop;
		Reload_Shell:
			TNT1 A 0 A_OverlayOffset(3, 6, 36, WOF_ADD);
			SHOT GGGGGG 1 A_OverlayOffset(3, -2, -6, WOF_ADD);
			SHOT GGGG 1 A_OverlayOffset(3, 1.5, 0, WOF_ADD);
			SHOT H 4;
			Stop;
		ReloadEnd:
			SHOT A 0 A_TakeInventory("GreenShotgunReload");
			SHOT AAAAAA 1 A_OverlayOffset(PSP_WEAPON, -10, 2, WOF_ADD);
			Goto Ready;
		NoReload:
			#### # 0 A_StartSound("noammo", starttime: 0.1);
			SHOT A 1 A_OverlayOffset(PSP_WEAPON, 0, 4, WOF_ADD);
			SHOT AAAA 1 A_OverlayOffset(PSP_WEAPON, 0, -1, WOF_ADD);
			SHOT A 4;
			Goto Ready;
		Flash:
			SHOT E 2 BRIGHT;
			Stop;
		Spawn:
			SHOT Z -1;
			Stop;
	}
}

Class GreenShotgunAmmo : Ammo
{
	//it sure is ammo.
	Default
	{
		Radius 20;
		Height 24;
		Inventory.PickupMessage "$BOX_AMMO_STUPID";
		Inventory.Amount 6;
		Inventory.MaxAmount 6;
		Inventory.Icon "PISTE0";
		+CASTSPRITESHADOW;
	}
	
	States
	{
		Spawn:
			SHOT F -1;
			Stop;
	}
}

Class GreenShotgunPump : Ammo
{
	Default
	{
		Inventory.Amount 1;
		Inventory.MaxAmount 1;
	}
}

Class GreenShotgunReload : Inventory 
{
	Default
	{
		Inventory.Amount 1;
		Inventory.MaxAmount 1;
	}
}