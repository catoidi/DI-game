Class DI_Projectile : DI_Actor
{
	mixin DI_ProjectileMixin;

	bool flatbullet, dobounce, dopiercing, isdying;
	
	double pierceamount;

	property FlatBullet : flatbullet; //combine with +FLATSPRITE to create fake 3d bullets.
	property DoBounce : dobounce;
	property DoPiercing : dopiercing;
	property PierceAmount : pierceamount;
	
	DI_Weapon plrwpn;

	array<Actor> impacted;
	array<DIProjectileInventory> items;
	
	int initdamage, justpierced;
	
	Default
	{
		DamageFunction GetFinalDamage();
		Projectile;
		BounceType "Doom";
		BounceFactor 0.0;
		WallBounceFactor 0.0;
		BounceCount 10;
		DI_Projectile.DoBounce true; //disable to use custom bouncing behavior.
		DI_Projectile.DoPiercing true; //disable this to allow original bRIPPER behavior.
		DI_Projectile.PierceAmount 1.0; //determines how many enemies the bullet can pierce through. values lower than 1.0 will reduce the bullet's damage.
		-NOGRAVITY;
		+BLOODSPLATTER;
		+FORCEXYBILLBOARD;
		+BOUNCEONACTORS;
		+RIPPER; //allows piercing code to work. not needed if piercing is disabled.
	}

	override void PostBeginPlay()
	{
		Super.PostBeginPlay();

		if (!di_notweapon) {TrackBulletSource();}
		CheckStatusChange();
		UpdateSize();
		if (flatbullet) {DI_SideBullet();}
		
		//CheckItemStats(); //called by thebrain handler.
	}
	
	override bool CanCollideWith(Actor other, bool passive)
	{
		if (dopiercing && !passive) 
		{
			if (other is "DI_PlayerBase") {return false;}
			if (impacted.Find(other) != impacted.Size()) 
			{
				//console.printf("IGNORING THIS ENEMY");
				return false;
			}
			else 
			{
				//console.printf("incoming valid hit: %s", other.GetClassName());
				impacted.Push(other);
				justpierced += 1;
				//pierceamount -= 1.0;
			}
		}
		
		return Super.CanCollideWith(other, passive);
	}
	
	override int SpecialBounceHit(Actor bounceMobj, Line bounceLine, readonly<SecPlane> bouncePlane)
	{
		int returnvalue = Super.SpecialBounceHit(bounceMobj, bounceLine, bouncePlane);
		if (dobounce)
		{
			if (bounceMobj) 
			{
				DoImpactActor(bBOUNCEONACTORS, bounceMobj);
				if (BounceFactor == 0.0) {returnvalue = 0;}
			}
			if (bounceLine) 
			{
				impacted.Clear();
				DoImpactWall(bBOUNCEONWALLS, bounceLine);
				if (WallBounceFactor == 0.0) {returnvalue = 0;}
			}
			if (bouncePlane) 
			{
				DoImpactFloor((bBOUNCEONFLOORS || bBOUNCEONCEILINGS), bouncePlane);
				if (BounceFactor == 0.0) {returnvalue = 0;}
			}
		}
		return returnvalue;
	}
	
	void CheckItemStats()
	{
		initdamage = GetWeaponDamage();
	
		//console.printf("checking for items...");
		for (int i = 0; i < items.Size(); ++i)
		{
			if (items[i])
			{
				//console.printf("\cqFound: %s", items[i].GetClassName());
				if (dobounce)
				{
					//console.printf("\cqReading Bounce Stats...");
					BounceFactor += items[i].GiveBounceFactor(plr);
					WallBounceFactor += items[i].GiveWallBounceFactor(plr);
				}
				if (dopiercing)
				{
					pierceamount += items[i].GivePiercing(plr);
				}
			}
		}
		
		//console.printf("bounce: %.2f", BounceFactor);
		
		if (dobounce)
		{
			if (BounceFactor == 0.0) 
			{
				bBOUNCEONFLOORS = false;
				bBOUNCEONCEILINGS = false;
				bBOUNCEONACTORS = false;
			}
			if (WallBounceFactor == 0.0)
			{
				bBOUNCEONWALLS = false;
			}
		}
		if (dopiercing)
		{
			if (pierceamount <= 1.0) 
			{
				bRIPPER = false;
				dopiercing = false;
			}
			else if (dobounce) {bBOUNCEONACTORS = false;}
		}
	}
	
	clearscope virtual double GetFinalDamage()
	{
		double piercemult = pierceamount - (justpierced - 1);
		if (piercemult > 1.0) {piercemult = 1.0;}
		if (!dopiercing) {piercemult = 1.0;}
		if (piercemult < 0.0) {piercemult = 0.0;}
		//console.printf("%.1f, %.1f", initdamage, piercemult);
		double returnvalue = initdamage * piercemult;
		//if ((returnvalue <= 1.0) && (initdamage >= 1.0)) {returnvalue = 1.0;}
		return returnvalue;
	}
	
	void DoImpactActor(bool bounced, Actor bounceMobj)
	{
		foreach(mo : items) {if (mo) {mo.ImpactActor(plr, bounced, bounceMobj);}}
		//console.printf("hit actor");
	}
	
	void DoImpactWall(bool bounced, Line bounceLine)
	{
		foreach(mo : items) {if (mo) {mo.ImpactWall(plr, bounced, bounceLine);}}
		//console.printf("hit wall");
	}
	
	void DoImpactFloor(bool bounced, readonly<SecPlane> bouncePlane)
	{
		foreach(mo : items) {if (mo) {mo.ImpactFloor(plr, bounced, bouncePlane);}}
		//console.printf("hit floor");
	}
	
	void TrackBulletSource()
	{
		//lets projectiles remember what weapon that it originated from.
		if (target is "DI_PlayerBase")
		{
			let plr = DI_PlayerBase(target);
			if (plr.player.readyweapon is "DI_Weapon") 
			{
				plrwpn = DI_Weapon(plr.player.readyweapon);
				wpn = plrwpn; //oopsie :)
			}
		}
	}

	void DI_SideBullet()
	{
		A_SpawnItemEX("DI_Tracer_Side", flags: SXF_NOCHECKPOSITION | SXF_ORIGINATOR | SXF_SETMASTER | SXF_TRANSFERSPRITEFRAME | SXF_TRANSFERRENDERSTYLE | SXF_TRANSFERTRANSLATION | SXF_TRANSFERSCALE);
	}

	override void Tick()
	{
		Super.Tick();
		if (flatbullet) {pitch = (-Vel.Z / Speed) * 90;}
		
		if (!Level.IsFrozen()) {BulletTick();}
	}
	
	virtual void BulletTick()
	{
		if (dopiercing)
		{
			if (justpierced > 0) 
			{
				pierceamount -= justpierced;
				justpierced = 0;
			}
		
			if ((pierceamount <= 0.0) && (!isdying))
			{
				if (impacted.Size() > 0)
				{
					actor lasthit = impacted[impacted.Size() - 1];
					if (lasthit.bNOBLOOD) 
					{
						SetStateLabel("Crash");
					}
					else
					{
						SetStateLabel("XDeath");
					}
				}
				else
				{
					SetStateLabel("Death");
				}
				isdying = true;
			}
			else if (pierceamount <= 1.0) 
			{
				bRIPPER = false;
			}
		}
	}
}

Class DI_Tracer : DI_Projectile
{
	//the projectile fired from DI_Shoot().
	Default
	{
		Mass 0;
		Gravity 0.1;
		Radius 2;
		Height 4;
		Speed 60;
		Scale 0.2;
		DI_Projectile.FlatBullet true;
		+FLATSPRITE;
		+BRIGHT;
		+USEBOUNCESTATE;
	}

	States
	{
		Spawn:
			TRAC A -1;
			Loop;
		Bounce.Floor:
		Bounce.Ceiling:
		Bounce.Wall:
			#### # 1 
			{
				A_SpawnItemEX("DI_BulletPuff", flags: SXF_NOCHECKPOSITION);
				if (invoker.wpn) {A_DIFakeStatusPuff(3.0 * invoker.wpn.wpn_bulletsize);}
			}
			#### # 0 A_Jump(256, "Spawn");
			Stop;
		Bounce.Actor:
			#### # 1
			{
				if (invoker.wpn) {A_DIFakeStatusPuff(3.0 * invoker.wpn.wpn_bulletsize);}
			}
			#### # 0 A_Jump(256, "Spawn");
			Stop;
		Crash:
		Death.Sky:
		Death:
			TNT1 A 1 
			{
				A_SpawnItemEX("DI_BulletPuff", flags: SXF_NOCHECKPOSITION);
				if (invoker.wpn) {A_DIFakeStatusPuff(3.0 * invoker.wpn.wpn_bulletsize);}
			}
			Stop;
		XDeath:
			TNT1 A 1 
			{
				A_SpawnParticleEX("fff59e", TexMan.CheckForTexture("PUFFA0"), STYLE_NONE, SPF_ROLL, 10, 8 + sqrt(invoker.GetWeaponDamage()), startroll: random(0, 359));
				if (invoker.wpn) {A_DIFakeStatusPuff(3.0 * invoker.wpn.wpn_bulletsize);}
			}
			Stop;
	}
}

Class DI_Tracer_Side : DI_Actor
{
	Default
	{
		Health 1;
		+INVISIBLE;
		+ROLLSPRITE;
		+WALLSPRITE;
		+NOCLIP;
		+NOGRAVITY;
	}

	bool finished;

	override void Tick()
	{
		Super.Tick();

		if (master)
		{
			SetOrigin(master.Pos, true);
			bBRIGHT = master.bBRIGHT;
			roll = -90 - master.pitch;
			angle = master.angle + 90;
			bINVISIBLE = false;
		}
		else if (!finished) 
		{
			SetStateLabel("Death");
			finished = true;
		}
	}

	States
	{
		Spawn:
			#### # -1;
			Loop;
		Death:
			TNT1 A 1 {bINVISIBLE = true;}
			Stop;
	}
}

Class DIProjectileInventory : Inventory
{
	override void AttachToOwner(Actor other)
	{
		if (other is "DI_Projectile")
		{
			let proj = DI_Projectile(other);
			if (proj.items.Find(self) == proj.items.Size())
			{
				//keep list of all projectile inventory items here.
				proj.items.Push(self);
				self.owner = proj;
				//console.printf("adding item to projectile");
			}
		}
	}
	
	virtual int GiveBonusDamage(DI_PlayerBase plr) {return 0;}
	virtual int GiveBonusDamageMult(DI_PlayerBase plr) {return 0.0;}
	virtual double GivePiercing(DI_PlayerBase plr) {return 0.0;}
	virtual double GiveBounceFactor(DI_PlayerBase plr) {return 0.0;}
	virtual double GiveWallBounceFactor(DI_PlayerBase plr) {return 0.0;}
	virtual void ImpactActor(DI_PlayerBase plr, bool bounced, Actor victim) {}
	virtual void ImpactWall(DI_PlayerBase plr, bool bounced, Line wall) {}
	virtual void ImpactFloor(DI_PlayerBase plr, bool bounced, readonly<SecPlane> ground) {}
}