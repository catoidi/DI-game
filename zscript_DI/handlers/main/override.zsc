extend class DIHandler
{
	override void WorldLoaded(WorldEvent e)
	{	
		++wavenum;
		++fakewavenum;
		
		++endless_capmult;
		++endless_ratemult;
		++endless_spawnmult;
		++endless_healthmult;
		++endless_damagemult;
		++endless_speedmult;
		
		ParseDIWAVE(e);
		//GetWaveData(e);
		//ParseWaveData(e);
		
		GetFloorTime();
		
		GetSupporters(); //make list of supporters to show on title screen.
	}

	override void WorldThingSpawned(WorldEvent e)
	{
		if (e.Thing != null)
		{
			if (e.Thing is "DISpawner") {SpawnerSpawned(e);}
			if (e.Thing is "DISpawnerSpot") {SpawnerSpotUpdate(e);}
			if ((e.Thing.target != null) && (e.Thing.target is "PlayerPawn"))
			{
				if (e.Thing is "DI_Drop") {CoinDropper(e);}
			}
			if (e.Thing.GetClassName() == "DI_WaveStarter") //if it ain't broke don't fix it
			{
				GetWaveData(e);
				ParseWaveData(e);
				waveinprogress = true;
				for (int i = 0; i < 8; ++i)
				{
					let p = players[i].mo;
					if ((p is "DI_PlayerBase") && (playeringame[i]))
					{
						DI_PlayerBase plr = DI_PlayerBase(p);
						plr.DIWave_Start();
					}
				}
				DoWaveEvent(e);
				e.Thing.Spawn("DI_WaveSpawnTimer");
			}
			if (e.Thing.GetClassName() == "DI_ClearPlayerItems")
			{
				for (int i = 0; i < 8; ++i)
				{
					let p = players[i].mo;
					if ((p is "DI_PlayerBase") && (playeringame[i]))
					{
						DI_PlayerBase plr = DI_PlayerBase(p);
						plr.DIWave_End();
					}
				}
			}
			if (e.Thing is "DI_Monster")
			{
				DIMonsterHandler(e);
			}
			if (e.Thing is "DIShop")
			{
				shoparray.Push(DIShop(e.Thing));
			}
			if (e.Thing is "DI_FloorActor")
			{
				GiveFloorTime(DI_FloorActor(e.Thing));
			}
			if (e.Thing is "DI_WaveSpawnTimer")
			{
				if (wavetimeractor) {ThrowAbortException("%s", "WAVE ERROR: Attmepted to start the same wave multiple times!");}
				else {wavetimeractor = DI_WaveSpawnTimer(e.Thing);}
			}
		}
	}
	
	override void WorldThingRevived(WorldEvent e)
	{
		if (e.Thing != null)
		{
			if (e.Thing is "DISpawnerSpot") {SpawnerSpotUpdate(e);}
		}
	}
	
	override void WorldThingDied(WorldEvent e)
	{
		if (e.Thing != null)
		{
			if (e.Thing.bIsMonster == true) {DIMonsterDied(e);}
			if (e.Thing is "DISpawnerSpot") {SpawnerSpotUpdate(e, false);}
			if (e.Thing.target != null) 
			{
				if (e.Thing is "DI_Monster")
				{
					DI_Monster dimon = DI_Monster(e.Thing);
					bool notweapon = dimon.di_notweapon;
					GivePlayerCombo(e, notweapon); //notweapon needs to be passed to all functions that involve fresh weapon kills.
					if (e.Thing.target is "DI_PlayerBase")
					{
						DI_PlayerBase plr = DI_PlayerBase(e.Thing.target);
						BoxDrops(e, plr, notweapon); //notweapon needs to be passed to all functions that involve fresh weapon kills.
						BonusArmorDrop(e, plr);
						BonusAmmoGiver(e, plr); //used for DI_LeadRecycling.
						
						ToastDrop(e, plr);
					}
				}
			}
		}
	}
	
	override void WorldThingDestroyed(WorldEvent e)
	{
		if (e.Thing != null)
		{
			if (e.Thing is "DISpawnerSpot") {SpawnerSpotUpdate(e, false);}
			if (e.Thing is "DIShop") 
			{
				int removeshop;
				foreach(shopmo : shoparray)
				{
					if (shoparray.Find(shopmo) < shoparray.Size()) {removeshop = shoparray.Find(shopmo);}
				}
				shoparray.Delete(removeshop);
			}
		}
	}
	
	override void WorldTick()
	{
		ammogivetick = false;
		
		++floortimer;
		if (floortimer > 210)
		{
			GetFloorTime();
			floortimer = 0;
		}
		
		if (Level.MapName ~== "titlemap") {TitlemapTick();}
	}
	
	override void RenderUnderlay(RenderEvent e)
	{
		if (supportwait >= 190) {ShowSupporters(e);}
	}
	
	override void NetworkProcess(ConsoleEvent e)
	{
		if (e.Name == "DIRerollShops")
		{
			foreach(shopmo : shoparray)
			{
				if (shopmo)
				{
					shopmo.SetStateLabel("Reroll");
				}
			}
		}
		else if (e.Name == "dieventtest")
		{
			console.printf("%s", "SWAG EVENT ACTIVATED!!!!!!!!!!");
		}
		else if (e.Name == "diaddenemycount") //allows other scripts to add to the handler's estimated enemy count.
		{
			enemycount += e.Args[0];
		}
		else if (e.Name.Left(16) == "diplayeritemused")
		{
			//code for DI_Consumer.
			let itemplr = players[e.Player].mo;
			if (itemplr)
			{
				int consumer = itemplr.CountInv("DI_Consumer");
				if (consumer > 0) 
				{
					itemplr.GiveInventory("DI_Regen", 3 * consumer);
					itemplr.GiveInventory("DI_Consumer_Powerup", 3 * consumer);
				}
			}
		}
		else if (e.Name == "wave_debug")
		{
			console.printf("waitnum: %d\ncurrentspawners: %d\nspawnqueue: %d\nalive monsters: %d\nenemycount: %d", waitnum, currentspawners, spawnqueue.Size(), Level.Total_Monsters - Level.Killed_Monsters, enemycount);
			//if ((waitnum == 0) && (currentspawners <= 0) && (spawnqueue.Size() == 0) && (queuefinished == true) && (Level.Total_Monsters == Level.Killed_Monsters + 1) && (enemycount <= 0))
		}
		else if (e.Name.Left(14) == "autoblacklist ")
		{
			name newblacklist = e.Name.Mid(14);
			foreach(mo : shoparray)
			{
				if (mo) {mo.AddBlacklist(newblacklist);}
			}
		}
		else if (e.Name.Left(13) == "addwaveenemy ")
		{
			array<string> newenemy;
			e.Name.Split(newenemy, " ");
			int datasize = newenemy.Size();
			if (datasize >= 2)
			{
				name addname = newenemy[1];
				int addnum = 0;
				string addspawner = "";
				if (datasize >= 3) {addnum = newenemy[2].ToInt(10);}
				if (datasize >= 4) {addspawner = newenemy[3];}
				
				AddWaveEnemy(addname, addnum, addspawner);
			}
			else {ThrowAbortException("\cqInvalid Add Wave Enemy Command: '%s'", e.Name);}
		}
		else if (e.Name.Left(13) == "addpermaitem ")
		{
			if (players[e.Player].mo && (sv_cheats == true))
			{
				Array<String> cmd;
				e.Name.Split(cmd, " ");
				int cmdnum = 1;
				if (cmd.size() == 3) {cmdnum = cmd[2].ToInt(10);}
				let plr = DI_PlayerBase(players[e.Player].mo);
				
				plr.GiveInventory(cmd[1], cmdnum);
				plr.BuyPermaItem(cmd[1], cmdnum);
				console.printf("Added %d %s to your inventory.", cmdnum, cmd[1]);
			}
			else {console.printf("%s", "You cannot use this command while cheats are disabled.");}
		}
		else if (e.Name.Left(15) == "checkpermaitem ")
		{
			if (players[e.Player].mo && (sv_cheats == true))
			{
				Array<String> cmd;
				e.Name.Split(cmd, " ");
				let plr = DI_PlayerBase(players[e.Player].mo);
				let num = plr.GetPermaItemAmount(cmd[1]);
				console.printf("You have %d of these.", num);
			}
		}
		else if (e.Name == "DIWaveFinished")
		{
			wavetimeractor.Destroy();
		}
	}
}